/* Oliver Kullmann, 15.7.2012 (Swansea) */
/* Copyright 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/RamseyTheory/Lisp/Schur/Numbers.mac
  \brief Handling of Schur numbers

Use by

oklib_load("OKlib/ComputerAlgebra/RamseyTheory/Lisp/Schur/Numbers.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/RamseyTheory/Lisp/Ramsey/Numbers.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lambda.mac")$


/* *****************
   * Schur numbers *
   *****************
*/

/* r is natural numbers >= 0: */
schur(r) := if r <= 4 then [1,2,5,14,45][r+1]
 elseif r=5 then [161,306]
 elseif r=6 then [537,1837]
 elseif r=7 then [1681,12860]
 else block([lb : max(3^(r-7)*(1681-1/2)+1/2,ceiling(44/89*89^(r/4))),
             ub : floor(r!*%e),
             rub : ramsey([2,create_list(3,i,1,r)])],
   if rub#unknown then ub : min(ub, rub-1),
   [lb, ub])$
/* Remarks:
 - This is A030126 in the OEIS (http://oeis.org/A030126).
 - For the general bounds see Theorems 8.8, 8.9 and Corollary 8.6 in
   [Ramsey Theory on the Integers; Landman, Robertson].
 - Schur showed that schur(r) >= 3*schur(r-1)-1 for r >= 1.
   The sequence s(r) := 3*s(r-1)-1 has the law s(r) = 3^r*(s(0)-1/2)+1/2.
   Since for r=7 we have the last concrete bound, we set s(0) = 1681.
 - For r = 5:
    - For the lower bound see certificates in
      Investigations/RamseyTheory/SchurProblems/plans/general.hpp.
    - The upper bound comes from ramsey([2,[3,3,3,3,3]]) <= 307
      ([Small Ramsey Numbers; Radziszowski, The Electronic Journal of
      Combinatorics, August 22, 2011], Page 34
      http://www.combinatorics.org/ojs/index.php/eljc/article/view/ds1/html).
 - For r = 6:
    - The upper bound comes from ramsey([2,[3,3,3,3,3,3]]) <= 1838
      ([Small Ramsey Numbers; Radziszowski, The Electronic Journal of
      Combinatorics, August 22, 2011]).
 - For r = 7:
    - The upper bound comes from ramsey([2,[3,3,3,3,3,3,3]]) <= 12861
      ([Small Ramsey Numbers; Radziszowski, The Electronic Journal of
      Combinatorics, August 22, 2011]).

*/


/* **********************
   * Weak Schur numbers *
   **********************
*/

/* r is natural numbers >= 0: */
wschur(r) := if r <= 4 then [1,3,9,24,67][r+1]
 elseif r=5 then [190,1631]
 elseif r=6 then [573,11743]
 else block([lb : firste(schur(r)), ub : floor(r!*r*%e)+1,
             rub : ramsey([2,create_list(4,i,1,r)])],
   if rub#unknown then ub : min(ub, rub-1),
   rub : ramsey([2,create_list(3,i,1,2*r)]),
   if rub#unknown then ub : min(ub, rub-1),
   [lb, ub])$


/* *****************************
   * Palindromic Schur numbers *
   *****************************
*/

/* Instead of "numbers" we have "number predicates", which are true for n
   iff the corresponding satisfiability-problem for n is satisfiable.
*/

/* r is natural numbers >= 0: */
pdschur(r) := if r <= 4 then [ltlb(1),ltlb(2),ltlb(5),ltlb(14),ltlb(45)][r+1]
 elseif r=5 then lambda([n], if n>306 then false elseif n=158 or n>=161 then unknown else true)
 else unknown$


/* r is natural numbers >= 0: */
pdwschur(r) := if r <= 4 then [ltlb(1),ltlb(3),ltlb(6),ltlb(15),lambda([n], if n >= 48 or n=45 or n=46 then false else true)][r+1]
 elseif r=5 then lambda([n], if n>1631 then false elseif n>=161 then unknown else true)
 else unknown$
/* Note that if a palindromic (ordinary) Schur-instance is known to be
   satisfiable for n mod 3 # 2, then also the weak palindromic Schur-instance
   for that n is satisfiable.
*/


/* **************************
   * Full symmetry-breaking *
   **************************
*/

/* r is natural numbers >= 0: */

schurfsb(r) := if r <= 4 then [1,2,5,14,45][r+1]
 elseif r=5 then [161,seconde(schur(5))]
 elseif r=6 then unknown
 elseif r=7 then unknown
 else unknown$

pdschurfsb(r) := if r <= 4 then [ltlb(1),ltlb(2),ltlb(5),ltlb(14),ltlb(45)][r+1]
 elseif r=5 then lambda([n], if n>seconde(schur(5)) then false elseif n<=157 or n=159 or n=160 then true else unknown)
 elseif r=6 then unknown
 elseif r=7 then unknown
 else unknown$


wschurfsb(r) := if r <= 4 then [1,3,9,24,67][r+1]
 elseif r=5 then [161,seconde(wschur(5))]
 else unknown$

pdwschurfsb(r) := if r <= 4 then [ltlb(1),ltlb(3),ltlb(6),ltlb(15),lambda([n], if n >= 48 or n=45 or n=46 then false else true)][r+1]
 elseif r=5 then lambda([n], if n>1631 then false elseif n<=158 or n=160 then true else unknown)
 else unknown$

/* Note that schurfsb(r) and wschurfsb(r) do not need predicates, since these
   problems are monotone.
   Satisfiability of a palindromic problem means satisfiability of the
   corresponding non-palindromic problem.
   And satisfiability of an ordinary problem means satisfiability of the
   corresponding weak problem, except for palindromic problems in case of
   n+1 divisible by 3, and also not not regarding full symmetry-breaking,
   since the weak form uses a different full symmetry-breaking.
*/


/* *****************
   * Modular forms *
   *****************
*/

/* Instead of "numbers" we have "number predicates", which are true for n
   iff the corresponding satisfiability-problem for n is satisfiable.
*/

/* r is natural numbers >= 0: */
mschur(r) := if r <= 4 then [ltlb(1),ltlb(2),ltlb(5),ltlb(14),ltlb(45)][r+1]
 elseif r=5 then unknown
 else unknown$
/* Consider a fixed n:
   If the modular problem for n is satisfiable, then also the ordinary problem.
   And if the palindromic (ordinary) problem is satisfiable, then so is the
   modular problem.
*/

/* r is natural numbers >= 0: */
wmschur(r) := if r <= 4 then [ltlb(1),ltlb(3),ltlb(7),ltlb(15),
   lambda([n], if n<45 or n=47 then true else false)][r+1]
 elseif r=5 then unknown
 else unknown$
/* Consider a fixed n:
   If the modular problem for n is satisfiable, then also the ordinary problem.
   And if the palindromic (ordinary) problem is satisfiable, then so is the
   modular problem.
*/
/* Note that if a modular (ordinary) Schur-instance is satisfiable, then also
   the weak modular Schur-instance for that n.
*/


/*!
\endhtmlonly
*/
