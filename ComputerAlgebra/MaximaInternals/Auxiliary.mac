/* Oliver Kullmann, 23.1.2010 (Swansea) */
/* Copyright 2010, 2011, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/MaximaInternals/Auxiliary.mac
  \brief Various functions helping to handle Maxima specialities

Use by

oklib_load("OKlib/ComputerAlgebra/MaximaInternals/Auxiliary.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$


/* ***********************
   * Properties of terms *
   ***********************
*/

/* Testing whether term p contains some element in CRE-form: */
tcre_p(p) := not is(totaldisrep(p)=p)$


/* ***************************
   * Simplification of terms *
   ***************************
*/

/* Helper-function for simplification of "simple" terms: */
simplify_st(t) := block([simp:true, simpsum:true],
 factor(radcan(ratsimp(trigreduce(trigsimp(minfactorial(makefact(t))))))))$
/* Equality between simple terms: */
simplify_eq(t1,t2) := is(equal(simplify_st(t1-t2), 0))$
/* Equality between lists (prerequisite: L1 is a list): */
simplify_eql(L1,L2) :=
 if not listp(L2) or length(L1)#length(L2) then false else
 every_s(lambda([p], simplify_eq(first(p),second(p))), map("[",L1,L2))$


/* Simplification of arbitrary terms t: */
simplify_t(t) := if atom(t) then t
 elseif op(t) = "=" then
   if listp(first(t)) then simplify_eql(first(t),second(t))
   elseif listp(second(t)) then false
   elseif {setp(first(t)),setp(second(t))} = {true,false} then false
   else simplify_eq(first(t),second(t))
 else simplify_st(t)$


/*!
\endhtmlonly
*/
