/* Oliver Kullmann, 7.6.2008 (Swansea) */
/* Copyright 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/NumberTheory/Lisp/Auxiliary.mac
  \brief Functions for number theory functions which more or less augment/correct Maxima functions

Use by

oklib_load("Transitional/ComputerAlgebra/NumberTheory/Lisp/Auxiliary.mac");

*/

/*!
\htmlonly
*/

/* **************
   * Logarithms *
   **************
*/

/* The binary logarithm. */
ld(x) := log(x) / log(2)$

/* The integer binary logarithm for natural numbers n (minf for n=0). */
/* Given a precise floor, this is just floor(ld(n)) (for n <> 0). */
fld(n) := if n = 0 then minf else 
 block([l : 0, b : 1], while b < n do (l : l + 1, b : b * 2),
  if b > n then return(l-1) else return(l))$

/* Returns the floor of the logarithm of n base b, correcting
   for issues in precision causing round off. Cannot handle large
   numbers. */
floorlog(n,b) := block([l : floor(float(log(n)/log(b)))], 
  if b^(l+1) <= n then (l+1) else
  if b^(l) > n then (l-1) else l)$ /* Handle Issues with precision */


/* ******************************
   * Representations of numbers *
   ******************************
*/

/* Converts a string to a list of characters: */
str2chrl(str) := makelist(charat(str, i),i, 1, slength(str))$

/* Converts a digit of the form "1" or "D" etc to an integer. Works up to a
   digit of base 36. */
digit2int(d) := 
  if cgreaterp("A",d) then (cint(d) - cint("0"))
  else (10 + (cint(d) - cint("A")))$

/* Converts a string representing a number in base N to an integer: */
base_n2int(str,n) := 
  lreduce(lambda([a,b], a * n + digit2int(b)),str2chrl(str),0)$

/* Converts an integer to a digit char in base N. N is not necessary as
   a parameter as this is for a single digit and so the integer is
   assumed to be within the correct range for the given base. */
int2digit(m) := 
    if m < 10 then ascii(cint("0") + m)
    else ascii(cint("A") + (m-10))$

/* Converts a given positive integer "m" to its base "n" representation. */
int2base_n(m,n) := 
  if m = 0 then "0" 
  else block([result : []],
    for ei : floorlog(m,n) step -1  thru 0 do block([dn], 
      dn : floor(m/n^ei), result : append(result,[int2digit(dn)]), m : m -dn*n^ei),
    return(simplode(result)))$



/* Extending integer_partitions from NN to ZZ. */
ext_integer_partitions(n) :=
 if n < 0 then {}
 elseif n = 0 then {[]}
 else integer_partitions(n)$

/* The corrected power-function: */
pow(b,e) := block([ze : is(equal(e,0))],
 if ze=true then return(1)
 elseif ze=false then return(b^e)
 else block([zb : is(equal(b,0))],
   if zb=true then return(if equal(e,0) then 1 else 0)
   elseif zb=false then return(b^e)
   else return(ev(pow(b,e),noeval))))$


/*!
\endhtmlonly
*/

