/* Oliver Kullmann, 30.4.2008 (Guangzhou) */
/* Copyright 2008, 2009, 2011, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def.mac
  \brief Classification of unsatisfiable hitting clause-sets in dependency on the deficiency, only considering clause-sets which are reduced w.r.t. singular DP-reduction

  Only small datasets here; others are included from for example
  "uhit_def_4.mac" for deficiency 4.

Use by

oklib_load("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/Deficiency2.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Symmetries/Symmetries.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/BasicOperations.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/Constructions.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ConflictCombinatorics/HittingClauseSets.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/tests/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Reductions/DP-Reductions.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/Deficiency3.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/Basics.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/HashMaps.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/Statistics.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$


/* kill(uhit_def)$ */

/* The list of available deficiencies. */
uhit_def[0,"info"] : append(create_list(k,k,1,28),[43,58,59])$

/* For a given deficiency k, the info-field uhit_def[k,"info"] contains:
 - true if the classification is complete, and false/unknown otherwise
 - the smallest possible n resp. a lower bound
 - the largest possible n resp. an upper bound
 - the list of available n-values, represented as pairs [n,true/false/unknown],
   stating whether or not for this n the list is complete.
*/
/* Complete means that every possible isomorphism type is
   represented (exactly once). */
/* "false" here means that definitely some cases are missing. */


/* Deficiency 1 */

uhit_def[1,"info"] : [true,0,0,[[0,true]]]$
uhit_def[1,0] : [{{}}]$


/* Deficiency 2 */

uhit_def[2,"info"] : [true,2,3,[[2,true],[3,true]]]$
uhit_def[2,2] : [musatd2_fcs(2)[2]]$
uhit_def[2,3] : [max_var_hitting_def(2)[2]]$


/* Deficiency 3 */

uhit_def[3,"info"] : [unknown,3,inf,[[3,true],[4,true],[5,unknown],[6,unknown],[7,unknown]]]$
uhit_def[3,3] : [
 minvdegdef3(2)[2],
 {{1,2,3},{-1,2,3},{1,-2,3},{-1,-2,3},{1,-3},{-1,-3}},
 {{-3,-2},{-3,-1,2},{-3,1,2},{-2,-1,3},{-2,1,3},{2,3}}
]$
uhit_def[3,4] : [
 {{-4,-3},{-4,-1,2,3},{-4,1,2,3},{-3,-2,4},{-2,-1,3},{-2,1,3},{2,4}},
 {{-4,-3},{-4,-1,2,3},{-4,1,2,3},{-3,4},{-2,-1,3},{-2,1,3},{2,3,4}},
 {{-4,-3},{-4,-1,2,3},{-4,1,3},{-3,-2,-1,4},{-2,-1,3},{-2,1,4},{2,4}},
 {{-4,-3},{-4,2,3},{-3,-2,1,4},{-3,-1,4},{-2,3},{-1,2,3,4},{1,2,4}},
 {{-4,-3},{-4,2,3},{-3,-1,4},{-3,1,4},{-2,-1,3},{-2,1,3},{2,3,4}},
 {{-4,-3,-2},{-4,-2,3},{-3,-2,-1,4},{-3,-2,1,4},{-2,3,4},{-1,2},{1,2}},
 {{-4,-3,-2},{-4,2},{-3,-1,4},{-3,1,4},{-2,-1,3},{-2,1,3},{2,3,4}},
 {{-4,-3,-1},{-4,-3,1,2},{-4,-2,-1,3},{-4,-2,1},{-4,2,3},{-3,4},{3,4}},
 {{1,2,3,4},{-1,2},{-2,3},{-3,1},{-1,-2,-3,4},{-4,1,2,3},{-1,-2,-3,-4}},
 {{-4,-3},{-4,-2,-1,3},{-4,-2,1,3},{-4,-1,2,3},{-4,1,2,3},{-3,4},{3,4}}
]$
uhit_def[3,5] : [
 vardisjoint_full_gluing(musatd2_fcs(2),musatd2_fcs(2))[2],
 {{1,2,3},{-1,2,3},{1,-2,4},{-1,-2,4},{2,-3,5},{2,-3,-5},{-2,-4,5},{-2,-4,-5}},
 {{-5,-4,-2},{-5,-2,3,4},{-4,-3,-2,5},{-3,-2,-1,4},{-3,-2,1,4},{-2,3,5},{-1,2},{1,2}},
 {{-5,-4,-2,-1},{-5,-4,-2,1},{-5,-2,-1,4},{-5,-2,1,4},{-5,2,3},{-3,-2,5},{-3,2},{3,5}},
 {{-5,-4},{-5,4},{-4,5},{-3,-2,-1,4,5},{-3,1,4,5},{-2,3,4,5},{-1,2,4,5},{1,2,3,4,5}},
 {{-5,-4},{-5,-3,4},{-4,-2,-1,3,5},{-4,-2,1,3,5},{-4,-1,2,3,5},{-4,1,2,3,5},{-3,5},{3,4}},
 {{-5,-4},{-5,4},{-4,-3,5},{-3,4,5},{-2,-1,3,5},{-2,1,3,5},{-1,2,3,5},{1,2,3,5}},
 {{-5,-4},{-5,4},{-4,-2,-1,3,5},{-3,-2,5},{-3,1,2,5},{-2,-1,3,4,5},{-1,2,5},{1,3,5}},
 {{-5,-4},{-5,4},{-4,-1,3,5},{-3,-2,5},{-3,2,5},{-2,1,3,5},{-1,3,4,5},{1,2,3,5}}
]$
uhit_def[3,6] : [
 vardisjoint_full_gluing(musatd2_fcs(2),musatd2_fcs(3))[2],
 {{-6,-1},{-6,1},{-5,-4,-3,-2,6},{-5,-4,2,6},{-4,-3,5,6},{-4,-2,3,6},{-4,2,3,5,6},{-1,4,6},{1,4,6}}
]$
uhit_def[3,7] : [
 max_var_hitting_def(3)[2]
]$



/* Deficiency 4 */

uhit_def[4,"info"] : [false,3,inf,[[3,true],[4,true],[5,unknown],[6,unknown],[7,unknown],[8,unknown],[9,unknown],[10,unknown],[11,unknown]]]$
uhit_def[4,3] : [
 nearly_full_hitting_fcs(3)[2]
]$
/* Remark: This is also minvdegdef4(2) (flipping variable 2). */
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def4.mac")$
uhit_def[4,11] : [
  max_var_hitting_def(4)[2]
]$


/* Deficiency 5 */

uhit_def[5,"info"] : [false,3,inf,[[3,true],[4,true],[5,false],[15,unknown]]]$
uhit_def[5,3] : [
 full_fcs(3)[2]
]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def5.mac")$
uhit_def[5,15] : [
  max_var_hitting_def(5)[2]
]$


/* Deficiency 6 */

uhit_def[6,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[19,unknown]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def6.mac")$
uhit_def[6,19] : [
 max_var_hitting_def(6)[2]
]$


/* Deficiency 7 */

uhit_def[7,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[23,unknown]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def7.mac")$
uhit_def[7,23] : [
 max_var_hitting_def(7)[2]
]$


/* Deficiency 8 */

uhit_def[8,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[27,unknown]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def8.mac")$
uhit_def[8,27] : [
 max_var_hitting_def(8)[2]
]$


/* Deficiency 9 */

uhit_def[9,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[31,unknown]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def9.mac")$
uhit_def[9,31] : [
 max_var_hitting_def(9)[2]
]$


/* Deficiency 10 */

uhit_def[10,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[35,unknown]]]$

uhit_def[10,4] : [
 {{-4,-3,-2},{-4,-3,-1,2},{-4,-3,1,2},{-4,-2,-1,3},{-4,-2,1,3},
  {-4,-1,2,3},{-3,-2,-1,4},{-3,-2,1,4},{-3,-1,2,4},{-3,1,2,4},
  {-2,-1,3,4},{-2,1,3,4},{-1,2,3,4},{1,2,3}},
 {{-4,-3,-2,-1},{-4,-3,-2,1},{-4,-3,-1,2},{-4,-2,-1,3},
  {-4,-2,1,3},{-4,-1,2,3},{-3,-2,-1,4},{-3,-2,1,4},{-3,-1,2,4},
  {-3,1,2},{-2,-1,3,4},{-2,1,3,4},{-1,2,3,4},{1,2,3}},
 {{-4,-3,-2,-1},{-4,-3,-1,2},{-4,-3,1},{-4,-2,-1,3},{-4,-2,1,3},
  {-4,-1,2,3},{-3,-2,-1,4},{-3,-2,1,4},{-3,-1,2,4},{-3,1,2,4},
  {-2,-1,3,4},{-2,1,3,4},{-1,2,3,4},{1,2,3}},
 {{-4,-3,-2,-1},{-4,-3,-1,2},{-4,-3,1,2},{-4,-2,-1,3},{-4,-2,1,3},
  {-4,-1,2,3},{-3,-2,-1,4},{-3,-2,1},{-3,-1,2,4},{-3,1,2,4},
  {-2,-1,3,4},{-2,1,3,4},{-1,2,3,4},{1,2,3}},
 {{-4,-3,-2,1},{-4,-3,-1,2},{-4,-3,1,2},{-4,-2,-1,3},{-4,-2,1,3},
  {-4,-1,2,3},{-3,-2,-1},{-3,-2,1,4},{-3,-1,2,4},{-3,1,2,4},
  {-2,-1,3,4},{-2,1,3,4},{-1,2,3,4},{1,2,3}}
]$
uhit_def[10,5] : [
 {{-5,-4,-3,-1},{-5,-4,-3,1},{-5,-3,-1,4},{-5,-3,1,4},{-5,-2,3},{-4,-3,2,5},
  {-4,-2,-1,5},{-4,-2,1,5},{-4,-1,2,3},{-4,1,2,3},{-3,-1,4,5},{-3,1,4,5},
  {-2,3,4,5},{-1,2,3,4},{1,2,3,4}}
]$
uhit_def[10,6] : [
 {{-6,-4,-1,3,5},{-6,-3,-2,1},{-6,-3,2,4},{-6,-2,-1,4,5},{-6,-1,2,3,4},
  {-6,1,3,5},{-5,-4,-1,2,3},{-5,-3,1,4,6},{-5,-2,-1},{-5,-1,2,4,6},{-5,1,3},
  {-4,-3,-2,-1,5},{-4,-3,-2,1,6},{-4,-3,2},{-4,3,5,6},{4,5,6}},
 {{-6,-5,-4,-2,-1},{-6,-5,-4,-2,1},{-6,-5,-3,2},{-6,-5,-2,-1,4},
  {-6,-5,-2,1,4},{-6,-2,3,5},{-5,-3,6},{-4,-3,-1,5},{-4,-3,1,5},{-4,-1,2,3},
  {-4,1,2,3},{-3,-1,4,5},{-3,1,4,5},{-2,3,6},{-1,2,3,4},{1,2,3,4}},
{{-6,-3,-2,5},{-6,-2,3},{-5,-4,-3,-1},{-5,-4,-3,1},{-5,-3,-1,4},{-5,-3,1,4},
 {-5,-2,3,6},{-4,-2,5,6},{-4,-1,2,3},{-4,1,2,3},{-3,-1,2,5},{-3,1,2,5},
 {-2,-1,4,5,6},{-2,1,4,5,6},{-1,2,3,4},{1,2,3,4}},
{{-6,-4,-3,5},{-6,-4,-2,3,5},{-6,-3,2,4},{-6,-2,4,5},{-5,-4,-1},{-5,-4,1},{-5,-3,2,4,6},
 {-5,-2,-1,4},{-5,-2,1,4},{-4,-1,2,3,5},{-4,1,2,3,5},{-3,-1,5,6},{-3,1,5,6},{-2,3,5,6},
 {-1,2,3,4},{1,2,3,4}},
{{-6,-5,-3,-1},{-6,-5,-3,1},{-6,-5,-1,3},{-6,-5,1,3},{-6,-2,4,5},{-6,2,5},{-5,-4,-2,3,6},
 {-5,-3,-2,-1,6},{-5,-3,-2,1,6},{-4,-2,-1,5},{-4,-2,1,5},{-4,2,6},{-3,-2,4,5,6},{-3,2,4,6},
 {-1,3,4,6},{1,3,4,6}},
{{-6,-5,-3,2},{-6,-4,-2,-1},{-6,-4,-2,1},{-6,-2,-1,4},{-6,-2,1,4},{-5,-3,6},{-5,-2,3,6},
 {-4,-2,5,6},{-4,-1,2,3},{-4,1,2,3},{-3,-1,2,5},{-3,1,2,5},{-2,-1,4,5,6},{-2,1,4,5,6},{-1,2,3,4},
 {1,2,3,4}},
{{-6,-5,-4,3},{-6,-3,-1},{-6,-3,1},{-6,-2,3,4},{-5,-4,-1,6},{-5,-4,1,6},{-5,-2,-1,4,6},
 {-5,-2,1,4,6},{-4,-3,5,6},{-4,-2,3,5},{-4,-1,2,3,5},{-4,1,2,3,5},{-3,2,4,6},{-2,4,5,6},
 {-1,2,3,4},{1,2,3,4}},
{{-6,-5,-4,-3,-1},{-6,-5,-4,-3,1},{-6,-5,-3,-1,4},{-6,-5,-3,1,4},{-6,-5,-2,3},{-6,-3,2,5},
 {-5,-4,-2,-1,6},{-5,-4,-2,1,6},{-5,-2,4,6},{-4,-1,2,3},{-4,1,2,3},{-3,2,6},{-2,-1,5},{-2,1,5},
 {-1,2,3,4},{1,2,3,4}},
{{-6,-3,-2,5},{-6,-2,3,5},{-5,-4,-3,-1},{-5,-4,-3,1},{-5,-3,-1,4},{-5,-3,1,4},{-5,-2,-1,3},
 {-5,-2,1,3},{-4,-2,5,6},{-4,-1,2,3},{-4,1,2,3},{-3,-1,2,5},{-3,1,2,5},{-2,4,5,6},{-1,2,3,4},
 {1,2,3,4}},
{{-6,-5,-4,-1},{-6,-5,-4,1},{-6,-4,-1,5},{-6,-4,1,5},{-6,-2,-1,4},{-6,-2,1,4},{-5,-1,2,3,4},
 {-5,1,2,3,4},{-4,-3,6},{-4,-2,3,6},{-4,-1,2,3,6},{-4,1,2,3,6},{-3,2,4},{-2,4,6},{-1,2,3,4,5},
 {1,2,3,4,5}}
]$
uhit_def[10,35] : [
  max_var_hitting_def(10)[2]
]$


/* Deficiency 11 */

uhit_def[11,"info"] : [false,4,inf,[[4,true],[6,unknown]]]$

uhit_def[11,4] : [nearly_full_hitting_fcs(4)[2]]$

uhit_def[11,6] : [
{{-6,-5,-3,2},{-6,-4,-2,-1},{-6,-4,-2,1},{-6,-2,-1,4},{-6,-2,1,4},
 {-5,-4,-3,-1,6},{-5,-4,-3,1,6},{-5,-3,-1,4,6},{-5,-3,1,4,6},{-4,-1,2,3},
 {-4,1,2,3},{-3,-2,5,6},{-3,-1,2,5},{-3,1,2,5},{-2,3,6},{-1,2,3,4},{1,2,3,4}},
{{-6,-4,-3,-1},{-6,-4,-3,1},{-6,-3,-1,4},{-6,-3,1,4},{-6,-2,3},{-5,-3,2,6},{-5,-2,4,6},
 {-4,-3,2,5,6},{-4,-2,-1,6},{-4,-2,1,6},{-4,-1,2,3},{-4,1,2,3},{-3,-1,4,5,6},{-3,1,4,5,6},
 {-2,3,4,5,6},{-1,2,3,4},{1,2,3,4}}
]$

/*
uhit_def[11,39] : [
  max_var_hitting_def(11)[2]
]$
*/


/* Deficiency 12 */

uhit_def[12,"info"] : [false,4,inf,[[4,true],[5,unknown],[6,unknown],[7,false]]]$
uhit_def[12,4] : [full_fcs(4)[2]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def12.mac")$
uhit_def[12,7] : [
  rbrouwer1999[2]
]$
/*
uhit_def[12,43] : [
  max_var_hitting_def(12)[2]
]$
*/


/* Deficiency 13 */

uhit_def[13,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def13.mac")$
/*
uhit_def[13,47] : [
 max_var_hitting_def(13)[2]
]$
*/


/* Deficiency 14 */

uhit_def[14,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def14.mac")$
/*
uhit_def[14,51] : [
 max_var_hitting_def(14)[2]
]$
*/


/* Deficiency 15 */

uhit_def[15,"info"] : [false,5,inf,[[5,false],[7,unknown]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def15.mac")$
/*
uhit_def[15,55] : [
 max_var_hitting_def(15)[2]
]$
*/


/* Deficiency 16 */

uhit_def[16,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def16.mac")$
/*
uhit_def[16,59] : [
 max_var_hitting_def(16)[2]
]$
*/


/* Deficiency 17 */

uhit_def[17,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def17.mac")$
/*
uhit_def[17,63] : [
 max_var_hitting_def(17)[2]
]$
*/


/* Deficiency 18 */

uhit_def[18,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def18.mac")$
/*
uhit_def[18,67] : [
 max_var_hitting_def(18)[2]
]$
*/


/* Deficiency 19 */

uhit_def[19,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def19.mac")$
/*
uhit_def[19,71] : [
 max_var_hitting_def(19)[2]
]$
*/


/* Deficiency 20 */

uhit_def[20,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def20.mac")$
/*
uhit_def[20,75] : [
 max_var_hitting_def(20)[2]
]$
*/


/* Deficiency 21 */

uhit_def[21,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def21.mac")$
/*
uhit_def[21,79] : [
 max_var_hitting_def(21)[2]
]$
*/


/* Deficiency 22 */

uhit_def[22,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def22.mac")$
/*
uhit_def[22,83] : [
 max_var_hitting_def(22)[2]
]$
*/


/* Deficiency 23 */

uhit_def[23,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def23.mac")$
/*
uhit_def[23,87] : [
 max_var_hitting_def(23)[2]
]$
*/


/* Deficiency 24 */

uhit_def[24,"info"] : [false,5,inf,[[5,false],[8,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def24.mac")$
uhit_def[24,8] : [
  brouwer1999[2]
]$
/*
uhit_def[24,91] : [
  max_var_hitting_def(24)[2]
]$
*/


/* Deficiency 25 */

uhit_def[25,"info"] : [false,5,inf,[[5,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def25.mac")$
/*
uhit_def[25,95] : [
 max_var_hitting_def(25)[2]
]$
*/


/* Deficiency 26 */

uhit_def[26,"info"] : [false,5,inf,[[5,true]]]$
uhit_def[26,5] : [nearly_full_hitting_fcs(5)[2]]$
/*
uhit_def[26,99] : [
 max_var_hitting_def(26)[2]
]$
*/


/* Deficiency 27 */

uhit_def[27,"info"] : [false,5,inf,[[5,true]]]$
uhit_def[27,5] : [full_fcs(5)[2]]$
/*
uhit_def[27,103] : [
 max_var_hitting_def(27)[2]
]$
*/

/* Deficiency 28 */

uhit_def[28,"info"] : [false,6,inf,[[6,false]]]$
uhit_def[28,6] : [over_full_hitting_fcs(5)[2]]$


/* Deficiency 43 */

uhit_def[43,"info"] : [false,6,inf,[[6,false]]]$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/MinimalUnsatisfiability/data/uhit_def43.mac")$
/*
uhit_def[43,127] : [
 max_var_hitting_def(43)[2]
]$
*/

/* Deficiency 58 */

uhit_def[58,"info"] : [false,6,inf,[[6,true]]]$
uhit_def[58,6] : [full_fcs(6)[2]]$
/*
uhit_def[58,227] : [
 max_var_hitting_def(58)[2]
]$
*/

/* Deficiency 59 */

uhit_def[59,"info"] : [false,7,inf,[[7,false]]]$
uhit_def[59,7] : [over_full_hitting_fcs(6)[2]]$



/* *************************************
   * Bounds on the deficiency and on n *
   *************************************
*/

/* For given number of variables n, the maximal deficiency k
  such that an unsatisfiable hitting clause-set with these parameters
  exists. This unique clause-set is full_fcs(n)[2], which is non-singular
  for n <> 1. */
max_def_uhit_n(n) := 2^n - n$

/* For given deficiency k, the conjectured ([OK,XSZ, 2008]) minimal n
   for which unsatisfiable non-singular hitting clause-sets exist: */
/* The smallest n with max_def_uhit_n(n) >= k. */
/* Using the Lambert-function lambertW(z,x) from Mupad, this is
   ceil(-k - lambertW(-1, -exp(-k))).
*/
min_n_uhit_def_i(k) := for n : 0 do if 2^n - n >= k then return(n)$
/* Alternative computation using the Lambert-W-function. */
/* This computation is much slower and also error-prone, but there might be
   some theoretical interest in it. */
min_n_uhit_def_l(k) := if k=1 then 0 else
 ceiling(-k - lambert_w_b(-2^(-k), 2))$
/* The branch "-1" of the Lambert-W-function: */
lambert_w1(z):= block([eps :  bfloat(10^(-fpprec+1))],
 z : bfloat(z),
 block(
  [w1, w2 : 0, p : bfloat(-sqrt(2*%e*z+2))],
   w1 : bfloat(((((((-221/8505*p)+769/17280)*p-43/540)*p+11/72)*p-1/3)*p+1)*p-1),
   while w2 - w1 >= eps do (w2 : w1, w1 : bfloat(log(z/w1))),
   return(w1)))$
/* Using this branch, the slightly generalised Lambert-W-function
   for base b (instead of e): */
lambert_w_b(x,b) := block([fpprec : 20],
 bfloat(lambert_w1(log(b) * x) / log(b)))$


/* ***************************
   * Accessing the catalogue *
   ***************************
*/

/* The list of all clause-sets in the catalogue for a given deficiency k >= 1.
   Sorted by ascending number of variables, and for a given number of variables
   in the given order.
*/
all_uhit_def(k) := block([L : uhit_def[k,"info"]],
  if op(L) = uhit_def then return([]),
  lappend(create_list(uhit_def[k,n],n,map(first,L[4]))))$


/* Apply function f to all elements of the catalogue:
   Call f(k,n,i,F), store the results in a list. */
apply_uhit(f) := block([list_def : uhit_def[0,"info"], result : []],
  for k in list_def do block([list_n : map(first,uhit_def[k,"info"][4])],
    for n in list_n do block([L : ev(uhit_def[k,n],eval)],
      for i : 1 thru length(L) do block([F : L[i]],
        result : endcons(f(k,n,i,F),result)))
  ),
  return(result))$


/* Collecting all clause-sets (into a set) from the uhit-catalogue for a
   given number of variables and a range of deficiencies.
   Prerequisite: k0, k1, n >= 0 are natural numbers.
*/
collect_uhit_n(n,k0,k1) :=
  setify(lappend(sublist(
    create_list(uhit_def[k,n],k,k0,k1),
    lambda([L], is(op(L) # uhit_def)))))$

/* Extracting the triple [n_data, c_data, def_data] from the uhit-catalogue:
    - n_data is the sorted list of occurring numbers of variables
    - c_data is a list of the same length, with entries true/false stating
      whether it is guaranteed that the collection is complete for the
      corresponding n-value
    - def_data: a list of pairs, showing for each n-value the min- and max-
      deficiencies.
*/
uhit_n_data() := block([D : uhit_def[0,"info"], h : sm2hm({}), res],
 for k in D do
  for p in uhit_def[k,"info"][4] do
    set_hm(h,first(p),cons([k,second(p)],ev_hm_d(h,first(p),[]))),
 set_hm(h,1,[]),
 res : listify(hm2sm(h)),
 [map(first,res),
  map(lambda([L], every(lambda([p],is(p[2]=true)),L)), map(second,res)),
  map(lambda([L], [lmin(map(first,L)),lmax(map(first,L))]), map(second,res))])$

/* The set of all known non-singular unsatisfiable hitting
   clause-sets for a given number of variables:
    - uhit_n(-1,something) yields the list of available n-values
    - uhit_n(n,true) = true/false, depending on wether the list is complete or
      not (as c_data above)
    - uhit_n(n,false) is the set of available clause-sets.
*/
uhit_n(n,info) := block([n_data, c_data, def_data],
 [n_data, c_data, def_data] : uhit_n_data(),
 if n = -1 then return(n_data)
 elseif not elementp(n,setify(n_data)) then
   if info then return(false) else return({})
 elseif info then return(c_data[n+1])
 else block([K : def_data[n+1]],
   if K=[inf,minf] then return({})
   else return(collect_uhit_n(n,K[1],K[2]))))$


/* ****************************
   * Evaluating the catalogue *
   ****************************
*/

/* For a list of candidate clause-sets, determine whether F is
   reduced unsatisfiable hitting, and in the positive case
   determine whether F is contained in the catalogue or not,
   where in the positive case the index-triple is returned.
   So for each F the corresponding entry in the return list
   is either false or [[k,n],i] or [[k,n],"new"].
*/
classify_candidates_uhit_def(L) := block([result : []],
  for F in L do block([check : check_hitting_nsing_def(F)],
    if length(check) = 0 then result : endcons(false,result)
    else block([element : [check[1], nvar_cs(F)], catalist],
      catalist : uhit_def[element[1], element[2]],
      if not listp(catalist) then result : endcons([element,"new"],result)
      else block([found : false],
        for i : 1 thru length(catalist) unless found do
          if is_isomorphic_btr_cs(F, catalist[i]) then (
            found : true,
            result : endcons([element, i],result)
          ),
        if not found then result : endcons([element,"new"],result)
      )
    )
  ),
  return(result))$

/* Similarly to analyse_isorepo_defset, but only keeps the clause-sets s.t.
   their min-var-degree is equal to the upper bound nonmersenne
   and which are not already in the uhit-catalogue. */
/* Repository must be an isomorphism-type repository of unsatisfiable
   non-singular hitting clause-sets. */
analyse_isorepo_defset_mvd(repository) := block(
 [M : hm2sm(repository), h : sm2hm({})],
  for P in M do block([def : P[1][2] - P[1][1], mvd],
    mvd : nonmersenne_rec[def],
    set_hm(h,def,union(ev_hm_d(h,def,{}),
      subset(map(fcs2cs,P[2]),
        lambda([F],is(min_variable_degree_cs(F)=mvd
          and classify_candidates_uhit_def([F])[1][2]="new")))))),
  sort(listify(hm2sm(h)),lambda([P1,P2], is(P1[1] < P2[1]))))$

/* This variation keeps those clause-sets which improve the min-var-degrees
   per deficiency in the catalogue. */
/* If the uhit-catalogue has been updated, then the memoised values of
   max_min_var_deg_uhit_def_mem might be wrong. */
analyse_isorepo_defset_imprmvd(repository) := block(
 [M : hm2sm(repository), h : sm2hm({})],
  for P in M do block([def : P[1][2] - P[1][1], mvd],
    mvd : max_min_var_deg_uhit_def_mem[def],
    if mvd < nonmersenne_rec[def] then (
      set_hm(h,def,union(ev_hm_d(h,def,{}),
        subset(map(fcs2cs,P[2]),
          lambda([F],is(min_variable_degree_cs(F) > mvd))))))),
  sort(listify(hm2sm(h)),lambda([P1,P2], is(P1[1] < P2[1]))))$


/* Compute the reduced isomorphism type of a clause-set F w.r.t.
   non-singular unsatisfiable hitting clause-sets:
    - [false] iff F is not unsatisfiable or not a hitting clause-set
      after singular DP-reduction
    - [[k,n],"new"] iff not in the catalogue
    - [[k,n],i] iff in the catalogue, with deficiency k, number of
      variables n and index i.
   According to [Kullmann, Zhao, 2008] for unsatisfiable hitting
   clause-sets F the result of sdp_reduction_cs(F) is unique
   up to isomorphism.
*/
redisotype_uhit_def(F) := classify_candidates_uhit_def(
  [sdp_reduction_cs(F)])[1]$


/* ****************************
   * Completing the catalogue *
   ****************************
*/

/* For all clause-sets in the catalogue with number of variables at most
   n_bound, apply all partial assignments, followed by iterated elimination
   of singular variables, and compute the non-isomorphic cases found not to
   be in the catalogue, in the form of a repository of isomorphism types,
   which is passed by name. Returned is the number of new clause-sets
   found in this way. */
closure_uhit_def_pass(n_bound, _hash_repo) := block(
 [min_n_unknown, /* the smallest n for which we don't have a
                         complete classification */
  count : 0],
  _hash_repo :: sm2hm({}),
  for n : 0 unless numberp(min_n_unknown) do
    if not uhit_n(n,true) then min_n_unknown : n,
  for n : min_n_unknown + 1 thru n_bound do
    for F in uhit_n(n,false) do
      for npass : 1 thru n - min_n_unknown do block([P : all_pass_n(setn(n),npass)],
        for phi in P do block(
         [G : sdp_reduction_cs(apply_pa(phi,F)), V],
          V : var_cs(G),
          if
            length(V) >= min_n_unknown and
             classify_candidates_uhit_def([G])[1][2] = "new" and
             manage_repository_isomorphism_types([V,G],ev(_hash_repo)) then
              count : count + 1
        )
      ),
  return(count))$
/* Recall that via hm2osm(_hasp_repo) one gets the hash-map as list. */

/* For a nonsingular unsatisfiable hitting clause-set F consider all
   "nonsingular splittings", that is splitting on one variable followed
   by elimination of singular variables. The result is, as above, a
   repository of isomorphism types (those not already in uhit_def),
   which is passed by name, and the number of new clause-sets found (however,
   here _hash_repo is not reset: start an empty repository with
   _hash_repo:sm2hm({})):
*/
nonsingular_splitting_uhit_def_cs(F, _hash_repo) := block(
 [V : var_cs(F), count : 0, G],
  for v in V do
   for x in [-v,+v] do (
     G : sdp_reduction_cs(apply_pa_cs({x},F)),
     if classify_candidates_uhit_def([G])[1][2] = "new" and
      manage_repository_isomorphism_types(cs2fcs(G),ev(_hash_repo)) then
       count : count + 1
   ),
  count)$

/* Computing the repository of isomorphism types not already in the
   uhit-cataloge for deficiency k, obtained by variable-disjoint full glueing
   of elements in the uhit-catalogue:
*/
vardisjoint_glueing_uhit_def(k) := block([h:sm2hm({}), L1,k2,L2,FF],
 for k1 : 2 thru k-1 do (
   L1 : all_uhit_def(k1),
   k2 : k+1-k1,
   if k2 < k1 then return(done),
   L2 : all_uhit_def(k2),
   for F1 in L1 do
    for F2 in L2 do (
      FF : vardisjoint_full_gluing(cs2fcs(F1),cs2fcs(F2)),
      if classify_candidates_uhit_def([fcs2cs(FF)])[1][2] = "new" then
        manage_repository_isomorphism_types(FF,h)
    )
 ),
 h)$

/*!
\endhtmlonly
*/
