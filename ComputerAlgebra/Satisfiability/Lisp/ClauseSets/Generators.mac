/* Oliver Kullmann, 29.11.2007 (Swansea) */
/* Copyright 2007 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/ClauseSets/Generators.mac
  \brief Functions for basic operations with clause-sets

Use by

oklib_load("Transitional/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/Generators.mac");

*/

/*!
\htmlonly
*/

oklib_include("Transitional/ComputerAlgebra/Hypergraphs/Lisp/Generators.mac")$
oklib_include("Transitional/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/BasicOperations.mac")$

/* ********************
   * Full clause-sets *
   ********************
*/

/* a formal full clause set with variable set {1,...,n} and all possible (full) clauses */
full_fcs(n) := if n=0 then [{}, {{}}] else [setn(n), map(setify, apply(cartesian_product, makelist({-i,i},i,1,n)))]$


/* ************************
   * Pigeon-hole formulas *
   ************************
*/

/* Variables are "php(i,j)", meaning that pigeon i is in hole j */
kill(php)$
declare(php, noun)$
declare(php, posfun)$
php_var(i,j) := nounify(php)(i,j)$

/* The set of variables for the pigeonhole formulas with m pigeons and n holes */
var_php(m,n) := setify(create_list(php_var(i,j), i,1,m, j,1,n))$

/* In the following m denotes the number of pigeons, n the number of holes */

/* no two pigeon in the same hole */
pigeons_different_holes(m,n) := setify(create_list({- php_var(i,k), - php_var(j,k)}, 
  i,1,m-1, j,i+1,m, k,1,n))$

/* every pigeon in one hole */
pigeons_in_holes(m,n) := setify(
  makelist(setify(makelist(php_var(i,j), j, 1, n)), i,1,m))$

/* the weak pigeon formula as formal clause-set */
weak_php(m,n) := [var_php(m,n), 
 union(pigeons_different_holes(m,n), pigeons_in_holes(m,n))]$

output_weak_php(m,n,filename) := 
  output_cs_f(
    sconcat("The weak pigeonhole principle: ", m, " pigeons, ", n, " holes."), 
    weak_php(m,n),
    filename)$


/* *******************
   * Sudoku problems *
   *******************
*/

/* Variables are "sdk(i,j,k)", meaning that field (i,j) carries number k */
kill(sdk)$
declare(sdk, noun)$
declare(sdk, posfun)$
sdk_var(i,j,k) := nounify(sdk)(i,j,k)$

/* The set of variables for Sudoku formulas of box-dimension p */
var_sdk(p) := block([N : p^2],
 setify(create_list(sdk_var(i,j,k), i,1,N, j,1,N, k,1,N)))$

/* In the following p denotes the box-dimension */

/* different numbers in each row */
sdk_different_rows(p) := block([N : p^2],
 setify(create_list({- sdk_var(i,j1,k), - sdk_var(i,j2,k)}, i,1,N, k,1,N, j1,1,N-1, j2,j1+1,N)))$

/* different numbers in each column */
sdk_different_columns(p) := block([N : p^2],
 setify(create_list({- sdk_var(i1,j,k), - sdk_var(i2,j,k)}, j,1,N, k,1,N, i1,1,N-1, i2,i1+1,N)))$


/* Given box coordinates 1 <= i,j <= p, the list of coordinates of fields */
sdk_positions_box(i,j,p) := block(
 [row_offset : p * (i-1), column_offset : p * (j-1)],
 create_list([row_offset + r, column_offset + c], r,1,p, c,1,p))$

/* different numbers in each box */
sdk_different_boxes(p) := block([N : p^2, L : []],
 for i : 1 thru p do
  for j : 1 thru p do block([P : sdk_positions_box(i,j,p)],
   for k : 1 thru N do
    for p1 : 1 thru N-1 do
     for p2 : p1+1 thru N do
      L : cons({- sdk_var(P[p1][1],P[p1][2],k), - sdk_var(P[p2][1],P[p2][2],k)}, L)),
 setify(L))$

/* every field carries a number */
sdk_all_fields(p) := block([N : p^2],
 setify(create_list(setify(makelist(sdk_var(i,j,k), k, 1, N)), i,1,N, j,1,N)))$


/* the sudoku problem (weak form) as formal clause-set */
weak_sdk(p) := [var_sdk(p), 
 union(sdk_different_rows(p), sdk_different_columns(p), sdk_different_boxes(p),
       sdk_all_fields(p))]$

output_weak_sdk(p,filename) := block([FF : standardise_fcs(weak_sdk(p))],
  output_cs_f_v(
    sconcat("The Sudoku problem (weak form) with box-dimension ", p, "."), 
    FF[1],
    filename,
    FF[2]))$


/* every row contains all numbers */
sdk_all_rows(p) := block([N : p^2],
 setify(create_list(setify(makelist(sdk_var(i,j,k),j,1,N)), i,1,N, k,1,N)))$

/* every column contains all numbers */
sdk_all_columns(p) := block([N : p^2],
 setify(create_list(setify(makelist(sdk_var(i,j,k),i,1,N)), j,1,N, k,1,N)))$

/* every box contains all numbers */
sdk_all_boxes(p) := block([N : p^2],
 setify(create_list(setify(makelist(sdk_var(p[1],p[2],k),p,sdk_positions_box(i,j,p))),
  i,1,p, j,1,p, k,1,N)))$

/* every field carries a different numbers */
sdk_different_fields(p) := block([N : p^2],
  setify(create_list({-sdk_var(i,j,k1), -sdk_var(i,j,k2)}, i,1,N, j,1,N, k1,1,N-1, k2,k1+1,N)))$


/* the sudoku problem (dual weak form) as formal clause-set */
dual_weak_sdk(p) := [var_sdk(p), 
 union(sdk_all_rows(p), sdk_all_columns(p), sdk_all_boxes(p),
       sdk_different_fields(p))]$

output_dual_weak_sdk(p,filename) := block([FF : standardise_fcs(dual_weak_sdk(p))],
  output_cs_f_v(
    sconcat("The Sudoku problem (dual weak form) with box-dimension ", p, "."), 
    FF[1],
    filename,
    FF[2]))$


/* the strong sudoku problem as formal clause-set */
strong_sdk(p) := [var_sdk(p), 
 union(weak_sdk(p)[2], dual_weak_sdk(p)[2])]$

output_strong_sdk(p,filename) := block([FF : standardise_fcs(strong_sdk(p))],
  output_cs_f_v(
    sconcat("The Sudoku problem (strong form) with box-dimension ", p, "."), 
    FF[1],
    filename,
    FF[2]))$


/* Measures for the translated formulas */

/* Number of variables */
n_var_sdk(p) := p^6$

/* List of clause-counts for weak_sdk as with ncl_list_f */
ncl_list_weak_sdk(p) := if p = 0 then [] elseif p = 1 then [[1,1]] else
 block([N : p^2], return([ [2, 3 * N^2 * binom(N,2) - N^2 * N * (p-1)], [N, N^2] ]))$
/* List of clause-counts for dual_weak_sdk as with ncl_list_f */
ncl_list_dual_weak_sdk(p) := if p = 0 then [] elseif p = 1 then [[1,1]] else
 block([N : p^2], return([[2, N^2 * binom(N,2)], [N, 3 * N^2]]))$
/* List of clause-counts for dual_weak_sdk as with ncl_list_f */
ncl_list_strong_sdk(p) := if p = 0 then [] elseif p = 1 then [[1,1]] else
 block([N : p^2], return([ [2, 4 * N^2 * binom(N,2) - N^2 * N * (p-1)], [N, 4 * N^2] ]))$

/* Total number of clauses */
ncl_weak_sdk(p) := block([L : ncl_list_weak_sdk(p)],
 return(sum(L[i][2],i,1,length(L))))$
ncl_dual_weak_sdk(p) := block([L : ncl_list_dual_weak_sdk(p)],
 return(sum(L[i][2],i,1,length(L))))$
ncl_strong_sdk(p) := block([L : ncl_list_strong_sdk(p)],
 return(sum(L[i][2],i,1,length(L))))$

/* Number of literal occurrences */
nlitocc_weak_sdk(p) := block([L : ncl_list_weak_sdk(p)],
 return(sum(L[i][1] * L[i][2],i,1,length(L))))$
nlitocc_dual_weak_sdk(p) := block([L : ncl_list_dual_weak_sdk(p)],
 return(sum(L[i][1] * L[i][2],i,1,length(L))))$
nlitocc_strong_sdk(p) := block([L : ncl_list_strong_sdk(p)],
 return(sum(L[i][1] * L[i][2],i,1,length(L))))$


/* ***************************
   * Van der Waerden problem *
   ***************************
*/

/* Boolean clause-sets, whose solutions are the partitionings of {1,...,n}
 into two parts such that none of them contains an arithmetic progression
 of size k */
VanDerWaerden2_cs_f(k,n) := block([G : ArithmeticProgressions(k,n)], 
  [G[1], union(G[2], comp_cs(G[2]))])$

output_VanDerWaerden2_cs_f(k,n,filename) := 
  output_cs_f(
    sconcat("Van der Waerden problem: 2 parts, arithmetic progressions of size ", k, ", ", n, " elements."), 
    VanDerWaerden2_cs_f(k,n),
    filename)$


/* *******************************************************
   * Minimally unsatisfiable clause-sets of deficiency 2 *
   *******************************************************
*/

/* The standard MUSAT(def=2) formulas where every literal occurs at least twice,
   for n >= 2 */
musatd2(n) := [setn(n), union({setify(makelist(i,i,1,n)), setify(makelist(-i,i,1,n))},
 setify(makelist({-i,i+1},i,1,n-1)), {{-n,1}})]$


/* **************************************************
   * Translated minimally unsatisfiable clause-sets *
   **************************************************
*/

/* The translations into MUSAT according to [Papadimitriou, Wolfe 1988] */

/* New variables are "mupw(i)", where i is the index of the clause in F */
kill(mupw)$
declare(mupw, noun)$
declare(mupw, posfun)$
mupw_var(i) := nounify(mupw)(i)$

/* Input a formal clause-set F, returns a formal clause-set G such that
   if F is satisfiable then G is satisfiable, while if F is unsatisfiable
   then G is minimally unsatisfiable.
   Thus USAT is reduced to MUSAT. */
usat_musat(FF) := if not freeof(mupw, FF) then error("usat_musat[ERROR]: input contains variable scheme mupw!") else
 block([L : listify(FF[2]), m : length(FF[2]), Y],
  Y : setify(makelist(mupw_var(i), i, 1, m)),
  return(
    [union(FF[1], Y), setify(append(
      makelist(union(L[i], disjoin(mupw_var(i), Y)), i, 1, m),
      create_list(union({-listify(L[i])[j], -mupw_var(i)}, disjoin(mupw_var(i), Y)), i,1,m, j,1,length(L[i])),
      create_list({-mupw_var(i), -mupw_var(j)}, i,1,m-1, j,i+1,m)))]
  ))$

/* Input a formal clause-set F, returns a formal clause-set GP such that
   if F is satisfiable then GP is minimally unsatisfiable, 
   while if F is unsatisfiable then GP is unsatisfiable but not minimally
   unsatisfiable. Thus SAT is reduced to MUSAT. */
sat_musat(FF) := block([m : length(FF[2]), Y, G],
 Y : setify(makelist(mupw_var(i), i, 1, m)),
 G : usat_musat(FF),
 return([G[1], union(G[2],{Y})]))$


/* ********************************************************
   * Max-Clique translated to "SHORT RES" and "SMALL MUS" *
   ********************************************************
*/

/* According to [Fellows, Szeider, Wrightson; 2006]. */

/* Variables are "srfswx(i)", "srfswy(i,j)" and "srfswz(v,i)" for natural
   numbers i,j and vertices v. */
kill(srfswx,srfswy,srfswz)$
declare(srfswx, noun, srfswy, noun, srfswz, noun)$
declare(srfswx, posfun, srfswy, posfun, srfswz, posfun)$
srfswx_var(i) := nounify(srfswx)(i)$
srfswy_var(i,j) := nounify(srfswy)(i,j)$
srfswz_var(v,i) := nounify(srfswz)(v,i)$

new_k(k) := binom(k,2) + 2 * k$

/* Input a graph G and k >= 0, output a formal clause-set FF 
   which contains a minimally unsatisfiable sub-clause-set of size 
     at most new_k(k) + 1 iff
   FF has a resolution-dag with at most new_k(k) non-source-nodes iff
   G contains a clique of size k. 

   Thus contains_us_bydef(maxclique_mures(G,k), new_k(k)+1, Solver) is true
   iff G contains a clique of size k.

   Furthermore FF is unsatisfiable iff G has at least one edge.
*/
/* Auxiliary functions: */
maxclique_start(k) := {setify(append(makelist(srfswx_var(i),i,1,k), create_list(srfswy_var(i,j), i,1,k-1, j,i+1,k)))}$
maxclique_edges(E,k) := setify(create_list({-srfswy_var(i,j), srfswz_var(listify(E[e])[1],i), srfswz_var(listify(E[e])[2],j)}, i,1,k-1, j,i+1,k, e,1,length(E)))$
/* Remark to maxclique_edges: the underlying paper is ambiguous here whether to use
the orientation of the edges or not (we do). */
maxclique_vertices(V,k) := setify(create_list({-srfswx_var(i), srfswz_var(V[v],i)}, i,1,k, v,1,length(V)))$
maxclique_cleanup(V,k) := setify(create_list({-srfswz_var(V[v],i)}, i,1,k, v,1,length(V)))$
/* The main function: */
maxclique_mures(G,k) := block([V : listify(G[1]), E : listify(G[2])],
 [union(setify(makelist(srfswx_var(i),i,1,k)), 
        setify(create_list(srfswy_var(i,j), i,1,k-1, j,i+1,k)),
        setify(create_list(srfswz_var(V[v],i), v,1,length(V), i,1,k))),
  union(maxclique_start(k),maxclique_edges(E,k),maxclique_vertices(V,k),maxclique_cleanup(V,k))])$

/* Measures for the translated formulas */

/* Number of variables */
n_var_maxclique_mures(G,k) := k + binom(k,2) + length(G[1]) * k$

/* List of clause-counts as with ncl_list_f */
ncl_list_maxclique_mures(G,k) := block([n : length(G[1]), m : length(G[2])],
 if k = 0 then [[0,1]]
 elseif k = 1 then [[1, n + 1], [2, n * k]]
 elseif k = 2 then [[1, n * k], [2, n * k], [3, binom(k,2) * m + 1]] else
 [[1, n * k], [2, n * k], [3, binom(k,2) * m ], [k + binom(k,2), 1]])$

/*!
\endhtmlonly
*/
