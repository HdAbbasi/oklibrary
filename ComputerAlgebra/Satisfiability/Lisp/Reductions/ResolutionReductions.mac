/* Oliver Kullmann, 24.5.2008 (Swansea) */
/* Copyright 2008, 2013 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Satisfiability/Lisp/Reductions/ResolutionReductions.mac
  \brief Special cases of applications of resolutions which yield reductions

Use by

oklib_load("OKlib/ComputerAlgebra/Satisfiability/Lisp/Reductions/ResolutionReductions.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Resolution/Basics.mac")$


/* ****************************
   * 2-Subsumption resolution *
   ****************************
*/

/* Checks whether a clause-set is reduced w.r.t. 2-subsumption resolution: */
redtsrp(F) := block(
 [LF : listify(F), l : length(F), found : false],
 for i : 1 thru l-1 unless found do block([C : LF[i]],
  for j : i+1 thru l unless found do
   if two_subsumption_resolvable(C, LF[j]) then found : true),
 return(not found))$
/* The variation where a list of length <= 1 is returned. */
redtsrl(F) := block(
 [LF : listify(F), l : length(F), R : []],
 for i : 1 thru l-1 unless not emptyp(R) do block([C : LF[i]],
  for j : i+1 thru l unless not emptyp(R) do
   if two_subsumption_resolvable(C, LF[j]) then R : [{C,LF[j]}]),
 return(R))$
/* Reduce F w.r.t. 2-subsumption resolution */
redtsr(F) := block([R : redtsrl(F)],
  while not emptyp(R) do (
    F : adjoin(resolvent_s(R[1]),setdifference(F,R[1])),
    R : redtsrl(F)),
  return(F))$


/*!
\endhtmlonly
*/

