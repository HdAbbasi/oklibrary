/* Oliver Kullmann, 24.12.2009 (Swansea) */
/* Copyright 2009, 2010 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/tests/ByteField.mac
  \brief Tests for Advanced Encryption Standard

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/tests/ByteField.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/TestSystem/Lisp/Asserts.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/ByteField.mac")$
oklib_include("OKlib/ComputerAlgebra/Algebra/Lisp/FiniteFields.mac")$
oklib_include("OKlib/ComputerAlgebra/MaximaInternals/Auxiliary.mac")$

kill(f)$

/* ******************
   * Representation *
   ******************
*/

okltest_rijn_polynomial(f) := block([x, GF_IRREDUCIBILITY_CHECK : true],
  assert(hipow(f,x) = 8),
  assert(gf_set(2,8,f) = true),
  for i : 0 thru 7 do
    assert(elementp(coeff(f,x,i),{0,1}) = true),
  true)$

okltest_rijn_stand(f) := block([x],
  assert(f(0) = 0),
  assert(f(1) = 1),
  assert(f(x^8) = x^4+x^3+x+1),
  assert(f(x^8 + x^4 + x^3 + x + 1) = 0),
  true)$

okltest_nat2polybin(f) := block([x],
  assert(f(0) = 0),
  assert(f(1) = 1),
  assert(f(2) = x),
  assert(f(3) = x+1),
  assert(f(4) = x^2),
  for e : 0 thru 5 do
    assert(f(2^e) = x^e),
  true)$

okltest_polybin2nat(f) := block([x],
  assert(f(0) = 0),
  assert(f(1) = 1),
  assert(f(x) = 2),
  assert(f(x+1) = 3),
  assert(f(x^2) = 4),
  for e : 0 thru 5 do
    assert(f(x^e) = 2^e),
  true)$

okltest_rijn_stand_vec(f) := (
  assert(f([]) = []),
  assert(f([0]) = [0]),
  assert(f([1]) = [1]),
  assert(f([1,0,0,0,0,1,1]) = [1,0,0,0,0,1,1]),
  assert(f([2]) = [0]),
  assert(f([3]) = [1]),
  assert(f([3,1,1,1,2]) = [1,1,1,1,0]),
  true)$

okltest_nat2vecbin(f) := (
  assert(f(0) = [0]),
  assert(f(1) = [1]),
  assert(f(2) = [1,0]),
  assert(f(3) = [1,1]),
  assert(f(4) = [1,0,0]),
  for e : 0 thru 10 do
    assert(f(2^e) = cons(1,create_list(0,i,1,e))),
  true)$

okltest_nat2vecbin_rijn(f) := (
  assert(f(0) = [0,0,0,0,0,0,0,0]),
  assert(f(1) = [0,0,0,0,0,0,0,1]),
  assert(f(2) = [0,0,0,0,0,0,1,0]),
  assert(f(3) = [0,0,0,0,0,0,1,1]),
  assert(f(4) = [0,0,0,0,0,1,0,0]),
  for e : 0 thru 7 do
    assert(f(2^e) = create_list(if i = 8 - e then 1 else 0, i, 1, 8)),
  true)$

okltest_vecbin2nat(f) := (
  assert(f([0]) = 0),
  assert(f([1]) = 1),
  assert(f([1,0]) = 2),
  assert(f([1,1]) = 3),
  assert(f([1,0,0]) = 4),
  for e : 0 thru 10 do
    assert(f(cons(1,create_list(0,i,1,e))) = 2^e),
  true)$

okltest_polybin2vecbin(f) := (
  assert(f(0) = [0]),
  assert(f(1) = [1]),
  assert(f(x) = [1,0]),
  assert(f(x+1) = [1,1]),
  assert(f(x^2) = [1,0,0]),
  for e : 0 thru 10 do
    assert(f(x^e) = cons(1,create_list(0,i,1,e))),
  true)$

okltest_polybin2vecbin_rijn(f) := (
  assert(f(0) = [0,0,0,0,0,0,0,0]),
  assert(f(1) = [0,0,0,0,0,0,0,1]),
  assert(f(x) = [0,0,0,0,0,0,1,0]),
  assert(f(x+1) = [0,0,0,0,0,0,1,1]),
  assert(f(x^2) = [0,0,0,0,0,1,0,0]),
  for e : 0 thru 7 do
    assert(f(x^e) = create_list(if i = 8 - e then 1 else 0, i, 1, 8)),
  true)$

okltest_vecbin2polybin(f) := (
  assert(f([0]) = 0),
  assert(f([1]) = 1),
  assert(f([1,0]) = x),
  assert(f([1,1]) = x+1),
  assert(f([1,0,0]) = x^2),
  for e : 0 thru 10 do
    assert(f(cons(1,create_list(0,i,1,e))) = x^e),
  true)$

okltest_polybin2mvecbin(f) := (
  assert(f(0) = matrix([0])),
  assert(f(1) = matrix([1])),
  assert(f(x) = matrix([1],[0])),
  assert(f(x+1) = matrix([1],[1])),
  assert(f(x^2) = matrix([1],[0],[0])),
  for e : 0 thru 10 do
    assert(f(x^e) = apply(matrix,cons([1],create_list([0],i,1,e)))),
  true)$

okltest_polybin2mvecbin_rijn(f) := (
  assert(f(0) = matrix([0],[0],[0],[0],[0],[0],[0],[0])),
  assert(f(1) = matrix([0],[0],[0],[0],[0],[0],[0],[1])),
  assert(f(x) = matrix([0],[0],[0],[0],[0],[0],[1],[0])),
  assert(f(x+1) = matrix([0],[0],[0],[0],[0],[0],[1],[1])),
  assert(f(x^2) = matrix([0],[0],[0],[0],[0],[1],[0],[0])),
  for e : 0 thru 7 do
    assert(f(x^e) = apply(matrix,
        create_list(if i = 8 - e then [1] else [0], i, 1, 8))),
  true)$

okltest_mvecbin2polybin(f) := (
  assert(f(matrix([0])) = 0),
  assert(f(matrix([1])) = 1),
  assert(f(matrix([1,0])) = x),
  assert(f(matrix([1,1])) = x+1),
  assert(f(matrix([1,0,0])) = x^2),
  for e : 0 thru 10 do
    assert(f(apply(matrix,cons(1,create_list(0,i,1,e)))) = x^e),
  true)$



/* **************
   * Operations *
   **************
*/

okltest_rijn_add(f) := block([x],
  assert(f(0,0) = 0),
  assert(f(x,x) = 0),
  assert(f(x,x^4+x^2+x+3) = x^4+x^2+1),
  true)$

okltest_rijn_mul(f) := block([x],
  assert(f(0,0) = 0),
  assert(f(0,x^2) = 0),
  assert(f(1,x^3+x^2+3) = x^3+x^2+1),
  assert(f(x^4,x^4) = x^4+x^3+x+1),
  assert(f(rijn_polynomial-1,rijn_polynomial-1) = 1),
  true)$

okltest_rijn_matmul(f) := block([x],
  assert(f(matrix(),matrix()) = matrix()),
  assert(f(matrix([0]),matrix([0])) = matrix([0])),
  assert(f(matrix([x,0],[0,x]),matrix([x,0],[0,x])) = matrix([x^2,0],[0,x^2])),
  assert(f(matrix([x,1],[x,x^2]),matrix([1,x^3],[x,0])) = matrix([0,x^4],[x^3+x,x^4])),
  assert(f(matrix([x^4,0],[0,x^4]),matrix([x^4,0],[0,x^4])) = matrix([x^4+x^3+x+1,0],[0,x^4+x^3+x+1])),
  true)$

okltest_rijn_inv(f) := block([x],
  assert(f(1) = 1),
  assert(f(x) = x^7+x^3+x^2+1),
  assert(tcre_p(f(x+x^2)) = false),
  true)$

okltest_rijn_einv(f) := (
  assert(f(0) = 0),
  assert(okltest_rijn_inv(f) = true),
  true)$

okltest_rijn_natadd(f) := (
  assert(f(0,0) = 0),
  assert(okltest_rijn_add(buildq([f], lambda([a,b], nat2polybin(f(polybin2nat(rijn_stand(a)),polybin2nat(rijn_stand(b))))))) = true),
  true)$

okltest_rijn_natmul(f) := (
  assert(f(0,0) = 0),
  assert(f(0,1) = 0),
  assert(f(1,1) = 1),
  assert(f(2,141) = 1),
  true)$

okltest_rijn_natinv(f) := (
  assert(f(0) = 0),
  assert(f(1) = 1),
  assert(f(2) = 141),
  assert(f(polybin2nat(rijn_stand(-1))) = polybin2nat(rijn_stand(-1))),
  true)$

okltest_rijn_vecadd(f) := (
  assert(f([0,0,0,0,0,0,0,0], [0,0,0,0,0,0,0,0]) = [0,0,0,0,0,0,0,0]),
  for i : 0 thru 7 do
    assert(f(nat2vecbin_rijn(2^i),nat2vecbin_rijn(0)) = nat2vecbin_rijn(2^i)),
  for i : 0 thru 7 do
    assert(f(nat2vecbin_rijn(2^i),nat2vecbin_rijn(2^i)) = nat2vecbin_rijn(0)),
  assert(f([1,0,1,1,0,1,0,0], [1,1,1,1,1,1,1,1]) = [0,1,0,0,1,0,1,1]),
  true)$

okltest_rijn_vecmul(f) := (
  assert(f([0,0,0,0,0,0,0,0], [0,0,0,0,0,0,0,0]) = [0,0,0,0,0,0,0,0]),
  assert(f([0,0,0,0,0,0,0,1], [0,0,0,0,0,0,0,1]) = [0,0,0,0,0,0,0,1]),
  for i : 0 thru 7 do
    assert(f(nat2vecbin_rijn(2^i),nat2vecbin_rijn(0)) = nat2vecbin_rijn(0)),
  for i : 0 thru 6 do
    assert(f(nat2vecbin_rijn(2^i),nat2vecbin_rijn(2)) = nat2vecbin_rijn(2^(i+1))),
  for i : 0 thru 3 do
    assert(f(nat2vecbin_rijn(2^i),nat2vecbin_rijn(2^i)) = nat2vecbin_rijn(2^(2*i))),
  assert(f(nat2vecbin_rijn(2^7),nat2vecbin_rijn(2)) = [0,0,0,1,1,0,1,1]),
  assert(f(nat2vecbin_rijn(2^4),nat2vecbin_rijn(2^4)) = [0,0,0,1,1,0,1,1]),
  assert(f(nat2vecbin_rijn(2^7), nat2vecbin_rijn(2^7)) = [1,0,0,1,1,0,1,0]),
  assert(f(nat2vecbin_rijn(56), nat2vecbin_rijn(32)) = [0,1,0,0,0,0,0,1]),
  true)$

okltest_rijn_vecinv(f) := (
  assert(f([0]) = [0,0,0,0,0,0,0,0]),
  assert(f([1]) = [0,0,0,0,0,0,0,1]),
  assert(f([0,0,0,0,0,1,0,1]) = [0,1,0,1,0,0,1,0]),
  assert(okltest_rijn_inv(
      buildq([f], lambda([a], vecbin2polybin(f(polybin2vecbin_rijn(a))))))),
  /* XXX */
  true)$


/*!
\endhtmlonly
*/
