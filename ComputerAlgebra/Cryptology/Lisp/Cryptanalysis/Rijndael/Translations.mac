/* Matthew Gwynne, 26.3.2008 (Swansea) */
/* Copyright 2008, 2009 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/Translations.mac
  \brief Cryptanalysis functions for translations of Rijndael into active clauses etc

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/Translations.mac");
  
*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/AdvancedEncryptionStandard.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/SboxAnalysis.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul2CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul3CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul11CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul13CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul14CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/FieldMul9CNF.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/data/SboxCNF.mac")$

/* NOTE : 

  In several functions such as aes_round_cp, and aes_key_expansion_cp "blocks"
  are mentioned, which are matrices (4x4 as standard) of bytes (8 bits / boolean
  variables used as a unit of computation within the AES.

  In this translation, currently, when given as a list of variables, each block 
  is considered column by column as is standard in the literature.

*/


/* ***************************************
   * Sets of Conditions                  *
   ***************************************
*/

oklib_plain_include(graphs)$

aes_var_ordering : hash_table()$

/* Resets variable numbering back to default: */
aes_reset_vars() := block(
  aes_var_ordering : hash_table(),
  aes_cnf_var_number : 0 
)$

aes_cnf_var_number : 0$

/* Generates a list of integer variables where the values n and m are used to
   determine the range of variables to return (using an offset to ensure new
   variables are returned for each call to this function), where "name" is used
   to ensure certain special variable cases (such as plaintext variables etc)
   are given identifiable variables: */
aes_make_vars_int(name,n,m) :=
  if name = "p" then create_list(i,i,n,m)
  else if name = "k" then create_list(i,i,128 + n,128 + m)
  else if name = "c" then create_list(i,i,256 + n,256 + m)
  else block([rl], 
    rl : create_list(i,i,aes_cnf_var_number + 3 * 128 + n,
      aes_cnf_var_number+3*128+m),
    aes_cnf_var_number : aes_cnf_var_number + (m-n+1),
    return(rl))$

/* Generates a list of variables of the form name_i where
   name is given and i ranges between n and m, offset by a given value to such
   that repeated calls to this function will yield new variables */
aes_make_vars_named(name, n,m) := block(
  [val : get_hash(name,aes_var_ordering), res], 
  if val = false then block(val : 0, set_hash(name,aes_var_ordering, 0)),
  res : create_list(eval_string(concat(name,string(i))),i,val + n, val + m),
  set_hash(name,aes_var_ordering,val+m), return(res))$

/* Allows overriding of variable naming to provide a direct clause set
   translation which seems MUCH quicker than substituting for named variables 
   in the final product within maxima. */
aes_make_vars : aes_make_vars_named;

/* Number of Rounds for the AES */
aes_num_rounds : 10$

/* Returns a set of conditions representing AES given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_cp([bits]) := block([pBits, kBits, cBits, ekBits,ib,ob, rs : {}],
  pBits : take_elements(128,bits),
  kBits : take_elements(128,rest(bits,128)),
  cBits : rest(bits,256),
  ekBits : aes_make_vars("ek",1, (aes_num_rounds+1)*128),
  /* Key Expansion */
  rs : adjoin(apply(aes_key_expansion_c,append(kBits, ekBits)),rs),
  ib : pBits,
  all_ob : aes_make_vars("o",1,(aes_num_rounds)*128),
  ob : take_elements(128,all_ob),
  /* Initial Round Key Addition */
  rs : adjoin(apply(aes_add_c,append(ib,take_elements(128,ekBits),ob)),rs),
  ib : ob,
  /* Rounds */
  for j : 1 thru (aes_num_rounds - 1) do block(
    ob : take_elements(128,rest(all_ob,j*128)),
    rs : adjoin(
      apply(aes_round_c,append(ib,take_elements(128,rest(ekBits,j*128)),ob)),
        rs),
    ib : ob
  ),
  /* Final Round */
  /* If we have a reduced round variant, truncate rather than generalise */
  ob : cBits,
  if aes_num_rounds = 10 then 
    rs : adjoin(apply(aes_final_round_c,append(ib,take_elements(128,
            rest(ekBits,aes_num_rounds*128)),ob)),rs)
  else
    rs : adjoin(apply(aes_round_c,append(ib,take_elements(128,
            rest(ekBits,aes_num_rounds*128)),ob)),rs),
  return(rs)
)$

/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_round_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : aes_make_vars("so", 1,128),
  rs : adjoin(apply(aes_subbytes_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("ro", 1,128),
  rs : adjoin(apply(aes_shiftrows_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("mo", 1,128),
  rs : adjoin(apply(aes_mixcolumns_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : oBits,
  rs : adjoin(apply(aes_add_c,append(iBits, rkBits, tBits)),rs),
  return(rs)
)$

/* Returns a set of conditions representing the final AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_final_round_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : aes_make_vars("so", 1,128),
  rs : adjoin(apply(aes_subbytes_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : aes_make_vars("ro", 1,128),
  rs : adjoin(apply(aes_shiftrows_c,append(iBits, tBits)),rs),
  iBits : tBits,
  tBits : oBits,
  rs : adjoin(apply(aes_add_c,append(iBits, rkBits, tBits)),rs),
  return(rs)
)$

/* Returns a set of conditions representing the AES Subbytes operation given
   128 plaintext variables and 128 output variables. */
aes_subbytes_cp([bits]) := block([iBits, rkBits, oBits,tBits,rs:{}],
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  for i : 0 thru 15 do
    rs : adjoin(apply(aes_sbox_c, 
      append( take_elements(8, rest(iBits,i*8)),
        take_elements(8, rest(oBits,i*8)))
    ),rs),
  return(rs)
)$

/* Returns a set of conditions representing the AES Shiftrows operation given
   128 plaintext variables and 128 output variables. */
aes_shiftrows_cp([bits]) := block([iBits, oBits],
  iBits : take_elements(128,bits),
  oBits : take_elements(128,rest(bits,128)),
  setify(map(lambda([a,b],aes_eq_c(a,b)), oBits, 
    apply(append,
      aes_shiftrows(partition_elements(iBits,8))))
    )
)$

/* Returns a set of conditions representing the AES Mixcolumns operation given
   128 plaintext variables and 128 output variables. */
aes_mixcolumns_cp([bits]) := block([iBits, oBits, nc],
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  nc : length(iBits)/32,
  setify(map(lambda([a,b], 
    apply(aes_mixcolumn_c,append(apply(append,a),apply(append,b)))),
    partition_elements(partition_elements(iBits,8),nc),
    partition_elements(partition_elements(oBits,8),nc)))
)$

/* Returns a set of conditions representing the AES Mixcolumn operation (the 
   individual operation applied to a single column in AES Mixcolumns) given
   128 plaintext variables and 128 output variables. */
aes_mixcolumn_cp([bits]) := block([iBits, oBits, nc,rs : {}],
  iBits : partition_elements(take_elements(32, bits),8),
  oBits : partition_elements(rest(bits, 32), 8),
  /* First element */
  for i : 0 thru 3 do block([mulAVars, mulBVars, addAVars, addBVars],
    mulAVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul2_c,
        append(iBits[mod(0 + i,4) + 1],mulAVars)), rs),
    mulBVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul3_c,
        append(iBits[mod(1 + i,4) + 1],mulBVars)), rs),
    addAVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, append(mulAVars,mulBVars, addAVars)), rs),
    addBVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, 
      append(iBits[mod(2 + i,4) + 1],iBits[mod(3 + i,4) + 1], addBVars)), rs),
    rs : adjoin(apply(aes_add_c, append(addAVars,addBVars, oBits[i+1])), rs)
  ),
  for i : 0 thru 3 do block(
    [mulAVars, mulBVars, mulCVars, mulDVars, addAVars, addBVars],
    mulAVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul14_c,
        append(oBits[mod(0 + i,4) + 1],mulAVars)), rs),
    mulBVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul11_c,
        append(oBits[mod(1 + i,4) + 1],mulBVars)), rs),
    mulCVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul13_c,
        append(oBits[mod(2 + i,4) + 1],mulCVars)), rs),
    mulDVars : aes_make_vars("mc", 1,8),
    rs : adjoin(apply(aes_mul9_c,
        append(oBits[mod(3 + i,4) + 1],mulDVars)), rs),
    addAVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, append(mulAVars,mulBVars, addAVars)), rs),
    addBVars : aes_make_vars("mc",1,8),
    rs : adjoin(apply(aes_add_c, 
      append(mulCVars,mulDVars, addBVars)), rs),
    rs : adjoin(apply(aes_add_c, append(addAVars,addBVars, iBits[i+1])), rs)
  ),
  return(rs)
)$

/* Generates a set of unit clauses (initially simpler than simplifying constants
   or making partial assignments) to set the given vars (representing the bits
   of AES round constants) to the round constant for round n */
aes_round_constant(ivars, n) := block([rc_el,rc_l],
  rc_el : xreduce(lambda([a,b],aes_field_mul_data[b,a+1]),
               create_list(2,i,1,(n -1)),1),
  rc_l : egf_coeffs(rijn_bit_field,egf_num2poly(rijn_byte_field,rc_el),7),
  setify(map(
    lambda([a,b], if b = 0 then {-a} else {a}),
    ivars, rc_l
  ))
)$

/* Returns a set of conditions representing the AES keyschedule operation given
   128 key variables and 128 * (aes_num_rounds + 1) output variables
   (consecutive lots of 128 bits representing the individual round keys).
   
   The first 128 variables in the list "bits" represent the key block for the 
   cipher, and then the (i+1)'th consecutive 128 variables in "bits" represents
   the round key block for the i'th round. 
   
   */
aes_key_expansion_cp([bits]) := block(
  [n_k : 4,n_b : 4, n_r : aes_num_rounds ,kBits, rkBits, nc,rs : {}, 
  sboxVars, addVars,fMat, kMat],
  /* Primary Variables */
  kBits : take_elements(128,bits),
  rkBits : rest(bits,128),
  fBits : take_elements(128,rkBits),
  /* Split into columns */
  cols :  partition_elements(partition_elements(rkBits,8),4),
  kMat : partition_elements(partition_elements(kBits,8),4),
  fMat : partition_elements(partition_elements(fBits,8),4),
  /* Make first round key equal */
  for j : 1 thru n_k do
    for i : 1 thru 4 do
      rs : adjoin(apply(aes_eq_c,append(kMat[j][i], fMat[j][i])),rs),
  /* Setup output bits */
  for j : n_k thru (n_b * (n_r +1)) - 1 do block(
    if mod(j, n_k) = 0 then block (
      sboxVars : aes_make_vars("ke",1,8),
      rs : adjoin(apply(aes_sbox_c, append(cols[j][2], sboxVars)), rs),
      addVars : aes_make_vars("ke",1,8),
      rs : adjoin(apply(aes_add_c,
        append(cols[j-n_k+1][1],sboxVars,addVars)),rs),
      rcVars : aes_make_vars("rc",1,8),
      rs : union(rs, aes_round_constant(rcVars,floor(j/n_k))),
      rs : adjoin(apply(aes_add_c, 
        append(rcVars,addVars, 
        cols[j+1][1])),rs),
      for i : 1 thru 3 do block(
        sboxVars : aes_make_vars("ke",1,8),
        rs : adjoin(apply(aes_sbox_c, 
          append(cols[j][mod(i+1,4)+1], sboxVars)), rs),
        rs : adjoin(apply(aes_add_c, 
          append(cols[j-n_k+1][i+1],sboxVars, cols[j+1][i+1])),rs)
      )
    )
    else for i : 1 thru 4 do 
      rs : adjoin( apply(aes_add_c,
        append(cols[j][i], cols[j-n_k+1][i],cols[j+1][i])),rs)
  ),
  return(rs)
)$

/* Returns a set of conditions representing addition within GF(2^n) given
   2 * n input variables (n bits to be added to the next n bits)
   and n output variables. */
aes_add_cp([bits]) := block([ nb : floor(length(bits) / 3)],
  xreduce(union, map(lambda([a,b,c], {{-a,-b,-c},{-a,b,c},{a,-b,c},{a,b,-c}}), 
    take_elements(nb,bits),
    take_elements(nb, rest(bits,nb)), rest(bits,nb*2))))$

/* Returns a set of conditions representing equivalence over 2 sets of bits,
   given 2 consecutive lots of n bits to be represented as logically by the set
   output conditions */
aes_eq_cp([bits]) := block([ nb : floor(length(bits) / 2)],
  xreduce(union, map(lambda([a,b], {{-a,b},{a,-b}}), 
    take_elements(nb,bits), rest(bits,nb))))$

/* Generates (and caches) the canonical CNF representation as a formal clause
   set of AES Field muliplication (within the Rijndael GF(2^8) field) by p 
   (given as an integer) with new variables. 
   
   New variables are introduced for each clause in the canonical DNF 
   representation (see dualts_fcl), where the variables are of the form dts(i)
   and i is the rank in the lexiographical ordering of the clauses.

   Caching here simply means that upon the first call to the function with
   argument "p", the result of the computation for that argument is stored 
   in a global variable "aes_mul_ts_CNF[p]" which will then be returned 
   immediately on subsequent calls to "aes_mul_ts_gen" with the same argument 
   to allow for reuse of "aes_mul_ts_gen" without the overhead of 
   recomputation.
 */
aes_mul_ts_CNF : create_list(false,i,1,256)$
aes_mul_ts_gen(p) := 
  if aes_mul_ts_CNF[p+1] = false then block([plus16 : lambda([x],x+16)],
      [mul_dnf : generate_full_aes_field_mul_dnf_fcs(p)],
    aes_mul_ts_CNF[p+1] : dualts_fcl(
      [listify(mul_dnf[1]),
       sort(
         listify(mul_dnf[2]),
         lambda([a,b], is(
           rank_lex_subsets(map(plus16,a),32) < 
             rank_lex_subsets(map(plus16,b),32)))
        )]
     ),
     return(aes_mul_ts_CNF[p+1])
  ) else aes_mul_ts_CNF[p+1]$
/* Returns a set of conditions representing multiplication by 02 or "x" in the
   Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
aes_mul2_cp : aes_mul2_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables*/
aes_mul2_pi_cp([bits]) := rename_fcs(FieldMul2CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul2_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(2),append(bits,aes_make_vars("mul2",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul3_cp : aes_mul3_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul3_pi_cp([bits]) := rename_fcs(FieldMul3CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul3_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(3),append(bits,aes_make_vars("mul3",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul14_cp : aes_mul14_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul14_pi_cp([bits]) := rename_fcs(FieldMul14CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul14_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(14),
      append(bits,aes_make_vars("mul14",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul11_cp : aes_mul11_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul11_pi_cp([bits]) := rename_fcs(FieldMul11CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul11_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(11),
      append(bits,aes_make_vars("mul11",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul13_cp : aes_mul13_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul13_pi_cp([bits]) := rename_fcs(FieldMul13CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul13_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(13),
      append(bits,aes_make_vars("mul13",1,256)))[2])$

/* Returns a set of conditions representing multiplication by 03 or "x + 1" in
   the Rijndael GF(2^8) field given the 8 input bits and 8 output bits */
/* MG : This should ideally take the CNF to be used */
aes_mul9_cp : aes_mul9_pi_cp$
/* Representation using small subsets of prime implicates and no new
   variables */
aes_mul9_pi_cp([bits]) := rename_fcs(FieldMul9CNF,bits)[2]$
/* Canonical representation using new variables */
aes_mul9_ts_cp([bits]) :=   
  setify(
    rename_fcl(aes_mul_ts_gen(9),append(bits,aes_make_vars("mul9",1,256)))[2])$

/* Generates (and caches) the canonical CNF representation as a formal clause
   set of AES Sbox operator with new variables. New variables are 
   introduced for each clause in the canonical DNF representation (see
   dualts_fcl), where the new variables are of the form dts(i) and i is the 
   rank in the lexiographical ordering of the clauses.

   Caching here simply means that upon the first call to the function, the 
   result of the computation is stored in a global variable "aes_sbox_ts_CNF"
   which will then be returned immediately on subsequent calls to
   "aes_sbox_ts_gen" to allow for reuse of "aes_sbox_ts_gen" without the
   overhead of recomputation.
 */
aes_sbox_ts_CNF : false;
aes_sbox_ts_gen() := 
  if aes_sbox_ts_CNF = false then block([plus16 : lambda([x],x+16)],
      [sbox_dnf : generate_full_aes_sbox_dnf_fcs()],
    aes_sbox_ts_CNF : dualts_fcl(
      [listify(sbox_dnf[1]),
       sort(
         listify(sbox_dnf[2]),
         lambda([a,b], is(
           rank_lex_subsets(map(plus16,a),32) < 
             rank_lex_subsets(map(plus16,b),32)))
        )]
     ), 
     return(aes_sbox_ts_CNF)
  ) else aes_sbox_ts_CNF$
/* Default the translation to use the old Sbox translation. */
aes_sbox_cp : aes_sbox_pi_cp$
/* Returns a set of conditions representing the AES Sbox given the 8 input bits
   and 8 output bits */
/* MG : This should ideally take the CNF to be used */
/* Representation using small subsets of prime implicates and no new
   variables */
aes_sbox_pi_cp([bits]) := rename_fcs(Sbox44ICCNF,bits)[2]$
/* Canonical representation using new variables */
aes_sbox_ts_cp([bits]) :=
  setify(
    rename_fcl(aes_sbox_ts_gen(),append(bits,aes_make_vars("sbox",1,256)))[2])$

/* ***************************************
   * Rewrite functions                  *
   ***************************************
*/

oklib_plain_include("opsubst")$

/* Takes a given set of conditions, a condition name, and a function to
   rewrite with and then produces a new set of conditions */
rewrite_condition(con_set, con_name, con_rewrite_f) := block(
  [num_con : 0, total : length(con_set)], 
  setify(uaapply(append,map(
    lambda([b], block(
      if (oklib_monitoring and (mod(num_con,100) = 0)) then block(
        print(con_name, " = ", num_con,"/",total)),
      num_con : num_con + 1,
      if not (op(b) = con_name) then [b] 
      else
        block([result],
          result : ev(opsubst(con_rewrite_f,con_name,b),eval),
          if setp(result) then listify(result) else [result]
      ))),
  listify(con_set)
  ))
))$

/* Given a set of conditions, applies all rewrite rules in a typical order that
   would result in a simple AES translation and returns the result condition 
   set (should be a clause set) . */
/* MG : A more general formal rewrite system is obviously needed rather than 
   this strict hard coded method */
rewrite_all(rs) := block(
  rs : rewrite_condition(rs, aes_key_expansion_c,aes_key_expansion_cp),
  rs : rewrite_condition(rs, aes_round_c, aes_round_cp),
  rs : rewrite_condition(rs, aes_final_round_c, aes_final_round_cp),
  rs : rewrite_condition(rs, aes_shiftrows_c, aes_shiftrows_cp),
  rs : rewrite_condition(rs, aes_mixcolumns_c, aes_mixcolumns_cp),
  rs : rewrite_condition(rs, aes_mixcolumn_c, aes_mixcolumn_cp),
  rs : rewrite_condition(rs, aes_subbytes_c, aes_subbytes_cp),
  rs : rewrite_condition(rs, aes_mul2_c, aes_mul2_cp),
  rs : rewrite_condition(rs, aes_mul3_c, aes_mul3_cp),
  rs : rewrite_condition(rs, aes_mul14_c, aes_mul14_cp),
  rs : rewrite_condition(rs, aes_mul11_c, aes_mul11_cp),
  rs : rewrite_condition(rs, aes_mul13_c, aes_mul13_cp),
  rs : rewrite_condition(rs, aes_mul9_c, aes_mul9_cp),
  rs : rewrite_condition(rs, aes_add_c, aes_add_cp),
  rs : rewrite_condition(rs, aes_eq_c, aes_eq_cp),
  rs : rewrite_condition(rs, aes_sbox_c, aes_sbox_cp),
  return(rs)
)$

/* Produces a CNF clause set representing 128-bit AES: */
aes_cnf_cs() := block([rs : {}, aes_make_vars : aes_make_vars_int],
  aes_reset_vars(),
  rs : apply(aes_cp, 
    append(aes_make_vars("p",1,128), aes_make_vars("k",1,128),
      aes_make_vars("c",1,128))),
  rs : rewrite_all(rs),
  return(rs)
)$

/* Produces a formal CNF clause set representing 128-bit AES */
aes_cnf_fcs() := block([aes_cs : {}, aes_make_vars : aes_make_vars_int],
  aes_cs : aes_cnf_cs(),
  [create_list(i,i,1,aes_cnf_var_number+3*128), aes_cs])$
output_aes_cnf(n) :=
  output_fcs(
    sconcat("AES, 128-bit key and block, ",aes_num_rounds,
      " Rounds SAT Translation"),
    aes_cnf_fcs(), n)$
output_aes_cnf_stdname() :=
  output_aes_cnf(sconcat("AES_R",aes_num_rounds,".cnf"))$

/* Takes a hexidecimal string in the notation typically used for Rijndael blocks
   (discussed in [Design of Rijndael] and in CryptoSystems plans) and given the
   variable list (assumed to be in the correct left to right bit ordering)
   generates a PA to the given variables to the given constant.  */
aes_hex2pa(hexStr, variableList) := block(
  setify(map(
    lambda([a,b], if b = 0 then -a else a),
        variableList, 
        apply(append,map(lambda([a], 
          egf_coeffs(rijn_bit_field,a,7)),hex2gf2t8l(hexStr)))
  ))
)$

/* ***********************************************
   * Constraint template rewrite translation     *
   ***********************************************
*/

/* The translation system given below is designed to replace the above
   translation system in it's entirety but must be introduced piecewise,
   with appropriate tests. */

/* AES variables - aes_v(i,id) */
/* id here is used as an identifier such as "ks" for key schedule bits, "o"
   for output bits etc. */
/* These are used throughout for all AES constraint templates */
kill(aes_v)$
declare(aes_v, noun)$
declare(aes_v, posfun)$
aes_var(i,id) := nounify(aes_v)(i,id)$

/* Given two integers n and m, a namespace, and
   a noun identifier, returns a list of AES variables of the form
   aes_v(i,id) for n <= i <= m. */
generate_aes_constraint_vars(n,m,namespace, id) :=
  res : map(namespace,create_list(aes_var(i,id),i,n,m))$

/* Given a list of constraint templates, returns a new list of constraint
   templates after applying the given rewrite map. */
aes_rewrite_all_ctl(rs,rewrite_map) := block(
  [rewrite_hash : osm2hm(rewrite_map),rs_new : []],
  for ct in rs do block([ctr],
    ctr : ev_hm(rewrite_hash,op(ct)),
    if ctr # false then
      rs_new : cons(aes_rewrite_all_ctl(apply(ctr[1],args(ct)),rewrite_map),rs_new)
    else 
      rs_new : cons([first(rs)],rs_new),
    rs : rest(rs)),
  return(lappend(rs_new)))$

/* ********************************************
   * Rewrite system functions                 *
   ********************************************
*/

/* Given a parent namespace namespace_p, a new namespace template
   namespace, and a list of variables, returns a new namespace, which
   consists of namespace_p composed with namespace, where namespace has
    the given list of arguments as it's arguments. */
construct_constraint_namespace(namespace_p,namespace,arg_l) :=
  buildq([arg_l,namespace,namespace_p],
      lambda([a],namespace_p(apply(namespace,cons(a,arg_l)))))$

/* Given a constraint template function, a list of arguments to the constraint
   template, and a list of constraint template parameters (namespace etc),
   returns a constraint template constructed from these parameters. */
apply_constraint_template(ct,bits,arg_l) := apply(ct, endcons(arg_l,bits))$

/* Predicate which returns true if every constraint template
   instance in the given list can be distinguished from every
   other based solely on the auxiliary arguments.

   Note, this is important, as namespaces, and variables are based on this
   and any created variable must be unique, even if it is created during
   the rewriting of different instances of the same constraint template.
*/
template_instance_distinguishable_p(ctl) :=
  is(length(map(lambda([a],[op(a),last(args(a))]),ctl)) =
     length(setify(map(lambda([a],[op(a),last(args(a))]),ctl))))$


/* ***********************************************
   * Constraint templates                        *
   ***********************************************
*/

/* Standard templates */

/* Constraint template rewrite bundle which does nothing
   (i.e the rewrite function it represents has the identity
   as it's namespace, and no variables are introduced by it's
   rewrite function). */
kill(zero_ct)$
declare(zero_ct,noun)$
zero_ctrb : [lambda([[a]],apply(zero_ct,a)), lambda([a],a),lambda([a,b],[])]$

/* AES templates */

/* AES constraint template */
/* This template takes 384 arguments plus the auxiliary argument list.

   Each of the 384 arguments should be a variable, representing
   128 plaintext bits, 128 key bits, and 128 ciphertext bits as input
   to the AES encryption algorithm.

   The argument list should consist of the parent namespace, and
   the number of rounds.
*/
kill(aes_ct)$
declare(aes_ct,noun)$
/* AES key expansion template */
/* This template takes the 128 * (number of rounds + 2) arguments plus the
   auxiliary argument list.

   Each of the standard arguments should be a variable, representing
   128 key bits and the round key bits for each round (one after the other) as
   input to the AES key schedule.

   The argument list should consist of the parent namespace, and
   the number of rounds.
*/
kill(aes_key_expansion_ct)$
declare(aes_key_expansion_ct,noun)$
/* AES addition template */
/* This template takes any number of standard arguments divisble by three
   followed by the auxilliary argument list.

   The template represents the constraint which xors the first third
   of the standard arguments, with the second third, with the result
   as the third third.

   The argument list should consist of the parent namespace, the number
   of standard arguments, and any number of additional arguments
   which help to uniquely identify this constraint template instance
   from others.
*/
/* MG: This could become a more general xor/addition constraint if
   the rewrite system is used more globally in the library. */
kill(aes_add_ct)$
declare(aes_add_ct,noun)$
/* AES round template */
/* This template takes 384 arguments plus the auxiliary argument list.

   Each of the 384 arguments should be a variable, representing
   128 plaintext bits, 128 key bits, and 128 ciphertext bits as input
   to the AES round.

   The argument list should consist of the parent namespace, and
   the index of this round in the total number of rounds.
*/
kill(aes_round_ct)$
declare(aes_round_ct,noun)$
/* AES final round template */
/* This template takes the same arguments as the aes_round_ct template
   with the same meaning. */
kill(aes_final_round_ct)$
declare(aes_final_round_ct,noun)$
/* AES subbytes template */
kill(aes_subbytes_ct)$
declare(aes_subbytes_ct,noun)$
/* AES mixcolumns template */
kill(aes_mixcolumns_ct)$
declare(aes_mixcolumns_ct,noun)$
/* AES shiftrows template */
kill(aes_shiftrows_ct)$
declare(aes_shiftrows_ct,noun)$
/* AES sbox template */
kill(aes_sbox_ct)$
declare(aes_sbox_ct,noun)$

/* ***********************************************
   * Constraint template rewrite functions       *
   ***********************************************
*/

/* AES constraint rewrite bundle */
aes_ctrb : [aes_ctr_ctl,aes_namespace,aes_ns_var_l]$

/* Namespace */
kill(aes_ctr_ns)$
declare(aes_ctr_ns,noun)$
declare(aes_ctr_ns,posfun)$
aes_namespace([args]) := apply(nounify(aes_ctr_ns),args)$


/* Returns a list of the variables introduced when
   applying aes_ctr_ctl given the auxiliary parameter
   list arg_l, under the given rewrite_mapping.

   For example, given the constraint template instance

   aes_ct(1,...,384,arg_l)

   is rewritten using aes_ctr_ctl, under
   rewrite_mapping, the list of variables
   returned by this function are exactly those
   variables introduced by aes_ctr_ctl and
   any rewritten-subconstraints it introduces
   (mapped to the corresponding rewrite functions
   by variable mapping, that is, this function
   recursively generates such a variable list,
   based on sub-constraints it uses).
*/
aes_ns_var_l(arg_l,rewrite_mapping) := block(
  [aes_num_rounds,vars : [],namespace,
   rewrite_hash : osm2hm(rewrite_mapping),lookup_var_lf],
  lookup_var_lf : lambda([cta],
    ev_hm_d(rewrite_hash,cta,zero_ctrb)[3]),
  namespace : construct_constraint_namespace(
    arg_l[1],aes_namespace,rest(arg_l)),
  aes_num_rounds : arg_l[2],
  vars : cons(
    generate_aes_constraint_vars(1,(aes_num_rounds+1)*128,
      namespace,nounify(k)),vars),
  vars : cons(
    generate_aes_constraint_vars(1,aes_num_rounds*128,
      namespace,nounify(o)),vars),
  vars : cons(
    lookup_var_lf(aes_key_expansion_ct)(
      [namespace,aes_num_rounds],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_add_ct)([namespace,384],rewrite_mapping),
    vars),
  if aes_num_rounds = 10 then 
    vars : cons(
      lookup_var_lf(aes_final_round_ct)([namespace,10],rewrite_mapping),
      vars)
  else
    vars : cons(
      lookup_var_lf(aes_round_ct)([namespace,aes_num_rounds],rewrite_mapping),
      vars),
  for r : 1 thru aes_num_rounds - 1 do
    vars : cons(lookup_var_lf(aes_round_ct)([namespace,r],rewrite_mapping),
      vars),
  return(lappend(vars)))$
/* Rewrite function designed to rewrite the AES constraint template "aes_ct".

   Takes as arguments 385 variables, which are the arguments of the "aes_ct"
   instance being rewritten.

   The first 384 of these are 128 plaintext, 128 key and 128 ciphertext
   variables respectively. The final argument is a pair (list) of auxiliary
   arguments, the first being the namespace to place all variables introduced
   by this function within, and the second being the integer number of rounds
   which specifies which round-variant of AES is being rewritten.

   The result of this function is a list of constraint template instances
   representing the AES constraint template instance being rewritten.
*/
aes_ctr_ctl([arg_l]) := block(
  [bits, ct_arg_l, pBits, kBits, cBits, ekBits, ib, ob, rs : [],namespace,
  aes_num_rounds],
  ct_arg_l : last(arg_l),
  aes_num_rounds : ct_arg_l[2],
  bits : rest(arg_l,-1),
  namespace : construct_constraint_namespace(
    ct_arg_l[1],aes_namespace,rest(ct_arg_l)),
  pBits : take_elements(128,bits),
  kBits : take_elements(128,rest(bits,128)),
  cBits : rest(bits,256),
  ekBits :
    generate_aes_constraint_vars(
      1,(aes_num_rounds+1)*128,namespace,nounify(k)),
  /* Key Expansion */
  rs : cons(
    apply_constraint_template(
      aes_key_expansion_ct,append(kBits,ekBits),[namespace,aes_num_rounds]),
    rs),
  ib : pBits,
  all_ob :
    generate_aes_constraint_vars(1,(aes_num_rounds)*128,namespace,nounify(o)),
  ob : take_elements(128,all_ob),
  /* Initial Round Key Addition */
  rs : cons(
    apply_constraint_template(
      aes_add_ct,append(ib,take_elements(128,ekBits),ob),[namespace,384]),
    rs),
  ib : ob,
  /* Rounds */
  for j : 1 thru (aes_num_rounds - 1) do block(
    ob : take_elements(128,rest(all_ob,j*128)),
    rs : cons(
      apply_constraint_template(aes_round_ct,
        append(ib,take_elements(128,rest(ekBits,j*128)),ob),
        append([namespace,j],rest(ct_arg_l,2))),
      rs),
    ib : ob
  ),
  /* Final Round */
  /* If we have a reduced round variant, truncate rather than generalise */
  ob : cBits,
  if aes_num_rounds = 10 then 
    rs : cons(
      apply_constraint_template(aes_final_round_ct,
        append(ib,take_elements(128,rest(ekBits,aes_num_rounds*128)),ob),
        append([namespace,aes_num_rounds],rest(ct_arg_l,2))),
      rs)
  else
    rs : cons(
      apply_constraint_template(aes_round_ct,
        append(ib,take_elements(128,rest(ekBits,aes_num_rounds*128)),ob),
        append([namespace,aes_num_rounds],rest(ct_arg_l,2))),
      rs),
  return(rs)
)$

/*
   AES Round

*/

aes_round_ctrb :
 [aes_round_ctr_ctl, aes_round_namespace, aes_round_ns_var_l]$

/* AES round namespace */
kill(aes_round_ns)$
declare(aes_round_ns,noun)$
declare(aes_round_ns,posfun)$
aes_round_namespace([args]) := apply(nounify(aes_round_ns),args)$

aes_round_ns_var_l(arg_l,rewrite_mapping) := block(
  [vars : [],namespace,
   rewrite_hash : osm2hm(rewrite_mapping),lookup_var_lf],
  lookup_var_lf : lambda([cta],
    ev_hm_d(rewrite_hash,cta,zero_ctrb)[3]),
  namespace : construct_constraint_namespace(
    arg_l[1],aes_round_namespace,rest(arg_l)),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(so)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(ro)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(mo)),vars),
  vars : cons(
    lookup_var_lf(aes_subbytes_ct)([namespace],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_shiftrows_ct)([namespace],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_mixcolumns_ct)([namespace],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_add_ct)([namespace,384],rewrite_mapping),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_round_ctr_ctl([arg_l]) := block(
  [r,ct_arg_l,namespace,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : rest(arg_l,-1),
  ct_arg_l : last(arg_l),
  namespace : construct_constraint_namespace(
    ct_arg_l[1],aes_round_namespace,rest(ct_arg_l)),
  r : ct_arg_l[2],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(so)),
  rs : cons(
    apply_constraint_template(aes_subbytes_ct,
        append(iBits,tBits),
        [namespace]),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(ro)),
  rs : cons(
    apply_constraint_template(aes_shiftrows_ct,
        append(iBits,tBits),
        [namespace]),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(mo)),
  rs : cons(
    apply_constraint_template(aes_mixcolumns_ct,
      append(iBits, tBits),
      [namespace]),
    rs),
  iBits : tBits,
  tBits : oBits,
  rs : cons(
    apply_constraint_template(aes_add_ct,
      append(iBits, rkBits, tBits),
      [namespace,384]),
    rs),
  return(rs)
)$

/*
   AES Final round
   
*/

aes_final_round_ctrb :
 [aes_final_round_ctr_ctl, aes_final_round_namespace,aes_final_round_ns_var_l]$

/* AES final round namespace */
kill(aes_final_round_ns)$
declare(aes_final_round_ns,noun)$
declare(aes_final_round_ns,posfun)$
aes_final_round_namespace([args]) := apply(nounify(aes_final_round_ns),args)$

aes_final_round_ns_var_l(arg_l,rewrite_mapping) := block(
  [vars : [],namespace,
   rewrite_hash : osm2hm(rewrite_mapping),lookup_var_lf],
  lookup_var_lf : lambda([cta],
    ev_hm_d(rewrite_hash,cta,zero_ctrb)[3]),
  namespace : construct_constraint_namespace(
    arg_l[1],aes_final_round_namespace,rest(arg_l)),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(so)),vars),
  vars : cons(generate_aes_constraint_vars(1,128,namespace,nounify(ro)),vars),
  vars : cons(
    lookup_var_lf(aes_subbytes_ct)([namespace],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_shiftrows_ct)([namespace],rewrite_mapping),vars),
  vars : cons(
    lookup_var_lf(aes_add_ct)([namespace,384],rewrite_mapping),vars),
  return(lappend(vars)))$
/* Returns a set of conditions representing AES round given
   128 plaintext variables, 128 key variables and 128 output
   variables. */
aes_final_round_ctr_ctl([arg_l]) := block(
  [r,ct_arg_l,namespace,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : rest(arg_l,-1),
  ct_arg_l : last(arg_l),
  namespace : construct_constraint_namespace(
    ct_arg_l[1],aes_final_round_namespace,rest(ct_arg_l)),
  r : ct_arg_l[2],
  iBits : take_elements(128,bits),
  rkBits : take_elements(128,rest(bits,128)),
  oBits : rest(bits,256),
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(so)),
  rs : cons(
    apply_constraint_template(aes_subbytes_ct,
        append(iBits,tBits),
        [namespace]),
    rs),
  iBits : tBits,
  tBits : generate_aes_constraint_vars(1,128,namespace,nounify(ro)),
  rs : cons(
    apply_constraint_template(aes_shiftrows_ct,
        append(iBits,tBits),
        [namespace]),
    rs),
  iBits : tBits,
  tBits : oBits,
  rs : cons(
    apply_constraint_template(aes_add_ct,
      append(iBits, rkBits, tBits),
      [namespace,384]),
    rs),
  return(rs)
)$

/*
   Subbytes
   
*/

aes_subbytes_ctrb :
 [aes_subbytes_ctr_ctl,aes_subbytes_namespace,aes_subbytes_ns_var_l]$


/* AES subbytes namespace */
kill(aes_subbytes_ns)$
declare(aes_subbytes_ns,noun)$
declare(aes_subbytes_ns,posfun)$
aes_subbytes_namespace([args]) := apply(nounify(aes_subbytes_ns),args)$

aes_subbytes_ns_var_l(arg_l,rewrite_mapping) := block([namespace,
  rewrite_hash : osm2hm(rewrite_mapping),lookup_var_lf],
  lookup_var_lf : lambda([cta],
    ev_hm_d(rewrite_hash,cta,zero_ctrb)[3]),
  namespace : construct_constraint_namespace(
    arg_l[1], aes_subbytes_namespace,rest(arg_l)),
  lappend(create_list(
      lookup_var_lf(aes_sbox_ct)(
        append([namespace,i],arg_l),rewrite_mapping),i,1,16)))$
/* Returns a set of conditions representing the AES Subbytes operation given
   128 plaintext variables and 128 output variables. */
aes_subbytes_ctr_ctl([arg_l]) := block(
  [namespace,ct_arg_l,bits,iBits, rkBits, oBits,tBits,rs:[]],
  bits : rest(arg_l,-1),
  ct_arg_l : last(arg_l),
  namespace : construct_constraint_namespace(
    ct_arg_l[1], aes_subbytes_namespace,rest(ct_arg_l)),
  iBits : take_elements(128,bits),
  oBits : rest(bits,128),
  for i : 0 thru 15 do
    rs : cons(
      apply_constraint_template(aes_sbox_ct,
        append(
          take_elements(8, rest(iBits,i*8)),
          take_elements(8, rest(oBits,i*8))),
        [namespace,i+1]),
      rs),
  return(rs)
)$


/*!
\endhtmlonly
*/
