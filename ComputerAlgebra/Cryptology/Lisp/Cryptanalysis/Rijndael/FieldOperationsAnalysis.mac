/* Matthew Gwynne, 9.4.2008 (Swansea) */
/* Copyright 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac
  \brief Cryptanalysis functions and tools regarding AES/Rijndael field operations.

Use by

oklib_load("Transitional/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac");

*/

/*!
\htmlonly
*/

oklib_include("Transitional/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("Transitional/ComputerAlgebra/Cryptology/Lisp/Conversions.mac")$
oklib_include("Transitional/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/AdvancedEncryptionStandard.mac")$
oklib_include("Transitional/ComputerAlgebra/Satisfiability/Lisp/ConflictCombinatorics/HittingClauseSets.mac")$

/* ******************************************
   * Field Multiplication Analysis          *
   ******************************************
*/

/* Generation of the full DNF clause-set representing the 
   multiplication with the given constant field element (as
   a polynomial) in the Rijndael GF(2^8) field */
generate_full_aes_field_mul_dnf_fcs(constant_field_element) :=  
  [setify(create_list(i,i,1,16)),
   map(lambda([ce],binary_list_to_dnf_c(
    append(egf_coeffs(rijndael_bit_field,intToPoly(ce,2),8),
      egf_coeffs(rijndael_bit_field,intToPoly(
        aes_field_mul_data[constant_field_element,ce+1],
        2),8)))),
    setify(create_list(i,i,0,255)))]$

/* Generation of the full CNF clause-set representing the 
   multiplication with the given constant field element (as
   a polynomial) in the Rijndael GF(2^8) field */
generate_full_aes_field_mul_cnf_fcs(constant_field_element) := block(
  [full16cs : clauses_f(full_fcs(16)), aes_fieldmul_cs :
  clauses_f(generate_full_aes_field_mul_dnf_fcs(constant_field_element))],
  cs_to_fcs(setdifference(full16cs, map(comp_sl,aes_fieldmul_cs))))$

/* Tests whether a full CNF clause-set represents the AES field multiplication */
test_CNF_aes_field_mul(constant_field_element,FF) := 
 is(cs_to_fcs(dualtreehittingcls_fcs(FF,dll_heuristics_first_real)) =
   generate_full_aes_field_mul_dnf_fcs(constant_field_element))$

/* Generation of a CNF hitting clause-set, given a heuristics h for
   a backtracking solver (without reduction) */
hitting_cnf_aes_field_mul(constant_field_element,h) := 
  dualtreehittingcls_condensed_fcs(generate_full_aes_field_mul_dnf_fcs(constant_field_element),h)$

/*!
\endhtmlonly
*/
