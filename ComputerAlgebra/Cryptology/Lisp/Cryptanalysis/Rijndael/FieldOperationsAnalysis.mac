/* Matthew Gwynne, 9.4.2008 (Swansea) */
/* Copyright 2008, 2009, 2010, 2011 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac
  \brief Cryptanalysis functions and tools regarding AES/Rijndael field operations.

Use by

oklib_load("OKlib/ComputerAlgebra/Cryptology/Lisp/Cryptanalysis/Rijndael/FieldOperationsAnalysis.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ClauseSets/BasicOperations.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/ConflictCombinatorics/HittingClauseSets.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/ByteField.mac")$
oklib_include("OKlib/ComputerAlgebra/Cryptology/Lisp/CryptoSystems/Rijndael/SmallScaleWordFields.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/FiniteFunctions/Permutations.mac")$
oklib_include("OKlib/ComputerAlgebra/Satisfiability/Lisp/Generators/Generators.mac")$


/* ******************************************
   * Rijndael field multiplication analysis *
   ******************************************
*/

/* Generation of the (unique, full) DNF clause-set representing the 
   multiplication with the field element a (as an integer) in the 
   Rijndael GF(2^8) field.
   Prerequisite: 1 <= a <= 255.
*/
rijnmult_fulldnf_fcl(a) :=
  perm2dnffcl(1+map(lambda([b], rijn_natmul(a,b)), create_list(i,i,0,255)))$
rijnmult_fulldnf_fcs(a) := fcl2fcs(rijnmult_fulldnf_fcl(a))$

/* Generation of the full CNF clause-set representing the 
   multiplication with the given field element (as an integer) in the
   Rijndael GF(2^8) field.
   Prerequisite: 1 <= field_element <= 255.
*/
rijnmult_fullcnf_fcs(field_element) := block(
  [full16cs : fcs2cs(full_fcs(16)), 
   aes_fieldmul_cs : fcs2cs(rijnmult_fulldnf_fcs(field_element))],
  cs2fcs(setdifference(full16cs, map(comp_sl,aes_fieldmul_cs))))$


/* Output the full CNF clause-set of the permutation given by multiplication
   with a field-element different from zero:
*/
output_rijnmult_fullcnf_stdname(field_element) :=
  output_fcs(
    sconcat("Full CNF representation of the AES byte-field multiplication by ",field_element, "."),
    rijnmult_fullcnf_fcs(field_element),
    sconcat("AES_byte_field_mul_full_",field_element,".cnf"))$
          

/* Tests whether a formal CNF clause-set FF represents an AES field
   multiplication: */
rijnmult_cnfp(field_element,FF) := 
 is(cs2fcs(dualtreehittingcls_fcs(FF,dll_heuristics_first_real)) =
   rijnmult_fulldnf_fcs(field_element))$

/* *********************************************
   * Small scale field multiplication analysis *
   *********************************************
*/

/* Generation of the (unique, full) DNF clause-set representing the 
   multiplication with the field element a (as an integer) in the 
   small scale GF(b^e) field with the given parameters.
   Prerequisite: 0 <= a <= b^e-1 (integer).
*/
ssmult_fulldnf_fcl(a,b,e,mod_poly) :=
perm2dnffcl(1+map(lambda([ap], ss_natmul(a,ap,b,mod_poly)),
    create_list(i,i,0,b^e-1)))$
ssmult_fulldnf_fcs(a,b,e,mod_poly) := fcl2fcs(ssmult_fulldnf_fcl(a,b,e,mod_poly))$

/* Generation of the full CNF clause-set representing the 
   multiplication with the given field element (as an integer) in the
   small scale GF(b^e) field.
   Prerequisite: 1 <= field_element <= b^e-1.
*/
ssmult_fullcnf_fcs(field_element, b,e, mod_poly) := block(
  [fullcs : fcs2cs(full_fcs(2*e)), 
   aes_fieldmul_cs : fcs2cs(ssmult_fulldnf_fcs(field_element,b,e,mod_poly))],
  cs2fcs(setdifference(fullcs, map(comp_sl,aes_fieldmul_cs))))$
ssmult_fullcnf_fcs(field_element, b,e, mod_poly) := block(
  [fullcs : fcs2cs(full_fcs(2*e)), 
   aes_fieldmul_cs : fcs2cs(ssmult_fulldnf_fcs(field_element,b,e,mod_poly))],
  cs2fcs(setdifference(fullcs, map(comp_sl,aes_fieldmul_cs))))$


/* Output the full CNF clause-set of the permutation given by multiplication
   with a field-element different from zero:
*/
output_ssmult_fullcnf_stdname(field_element, b,e, mod_poly) :=
  output_fcs(
    sconcat("Full CNF representation of the Small scale AES byte-field multiplication by ",field_element, "."),
    ssmult_fullcnf_fcs(field_element, b,e, mod_poly),
    sconcat("ss_byte",b,"_",e,"_field_mul_full_",field_element,".cnf"))$
          

/* Tests whether a formal CNF clause-set FF represents an small scale AES
   field multiplication: */
ssmult_cnfp(field_element, FF, b, e, mod_poly) := 
 is(cs2fcs(dualtreehittingcls_fcs(FF,dll_heuristics_first_real)) =
   ssmult_fulldnf_fcs(field_element, b, e, mod_poly))$


/* *********************************************
   * Small scale field inversion analysis *
   *********************************************
*/

/* Generation of the (unique, full) DNF clause-set representing the 
   inversion in the small scale GF(b^e) field with the given parameters.
*/
ssinv_fulldnf_fcl(b,e,mod_poly) :=
perm2dnffcl(1+map(lambda([ap], ss_natinv(ap,b,mod_poly)),
    create_list(i,i,0,b^e-1)))$
ssinv_fulldnf_fcs(a,e,mod_poly) := fcl2fcs(ssinv_fulldnf_fcl(a,e,mod_poly))$

/* Generation of the full CNF clause-set representing the 
   inversion in the small scale field.
*/
ssinv_fullcnf_fcs(b,e, mod_poly) := block(
  [fullcs : fcs2cs(full_fcs(2*e)), 
   aes_fieldmul_cs : fcs2cs(ssinv_fulldnf_fcs(b,e,mod_poly))],
  cs2fcs(setdifference(fullcs, map(comp_sl,aes_fieldmul_cs))))$
ssinv_fullcnf_fcs(b,e, mod_poly) := block(
  [fullcs : fcs2cs(full_fcs(2*e)), 
   aes_fieldmul_cs : fcs2cs(ssinv_fulldnf_fcs(b,e,mod_poly))],
  cs2fcs(setdifference(fullcs, map(comp_sl,aes_fieldmul_cs))))$


/* Output the full CNF clause-set of the permutation given by inversion:
*/
output_ssinv_fullcnf_stdname(b,e, mod_poly) :=
  output_fcs(
    sconcat("Full CNF representation of the Small scale AES byte-field inversion."),
    ssinv_fullcnf_fcs(b,e, mod_poly),
    sconcat("ss_byte",b,"_",e,"_field_mul_full_.cnf"))$
          

/* Tests whether a formal CNF clause-set FF represents an AES field
   inversion: */
ssinv_cnfp(FF, b, e, mod_poly) := 
 is(cs2fcs(dualtreehittingcls_fcs(FF,dll_heuristics_first_real)) =
   ssinv_fulldnf_fcs(b, e, mod_poly))$

/* ******************************************
   * Representations by hitting clause-sets *
   ******************************************
*/

/* Generation of a CNF hitting clause-set, given a heuristics h for
   a backtracking solver (without reduction): */
rijnmult2hittingcnf_fcs(field_element,h) := 
  dualtreehittingcls_condensed_fcs(rijnmult_fulldnf_fcs(field_element),h)$

/*!
\endhtmlonly
*/
