/* Matthew Gwynne, 22.3.2008 (Swansea) */
/* Copyright 2008, 2009, 2012 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Algebra/Lisp/FiniteFields.mac
  \brief Finite field functions

Use by

oklib_load("OKlib/ComputerAlgebra/Algebra/Lisp/FiniteFields.mac");

*/

/*!
\htmlonly
*/

oklib_plain_include(gf)$

/* ***************************************
   * Finite Field Wrapper Functions      *
   ***************************************
*/

/* The gf_* functions are prefixed with an 'e' for 
   extended. These functions perform the same duty as their gf_*,
   counterparts, but require additionally an argument, that specifies
   which field the operation is being performed over, in the form [p,n,ip]
   where "p" is the prime characteristic of the field, "n" is the power to
   which "p" is raised, and "ip" the irreducible polynomial over GF(p).
   Or, alternatively, just [p,ip]; or even [p].
 */

/*
   This function provides a generic wrapper function, taking a function
   "func" and a list of arguments "args", with the field structure (defined 
   above) as the first element in "args", and the arguments to f as the 
   remaining elements.
*/
egf_func(func,args) := (
    apply(gf_set, first(args)),
    apply(func,rest(args)))$

egf_add([args]) := egf_func('gf_add, args)$
egf_sub([args]) := egf_func('gf_sub, args)$
egf_mul([args]) := egf_func('gf_mult, args)$
egf_div([args]) := egf_func('gf_div, args)$
egf_inv([args]) := egf_func('gf_inv, args)$
egf_matmul([args]) := egf_func('gf_matmult, args)$
egf_matinv([args]) := egf_func('gf_matinv, args)$
egf_exp([args]) := egf_func('gf_exp, args)$
egf_rand([args]) := egf_func('gf_rand, args)$
egf_findprim([args]) := egf_func('gf_primitive, args)$
egf_ind([args]) := egf_func('gf_index, args)$
egf_primep([args]) := egf_func('gf_primitive_p, args)$
egf_poly2num([args]) := egf_func('gf_p2n, args)$
egf_num2poly([args]) := egf_func('gf_n2p, args)$

/* Functions no longer implemented by Maxima: */

/* gf_coeffs(p, k), where p is a polynomial with integer coefficients, and
   k is an integer, is the list of coefficients of terms x^i, 0 <= i <= k,
   in the standard-representation of p, with constant coefficient last;
   p must be an polynomial in one indeterminate, and the result will be
   expressed in the indeterminate used in gf_set:
*/
gf_coeffs(p,k) := block([x,V],
 p : gf_add(0,p),
 V : listofvars(p),
 if not emptyp(V) then x : first(V),
 create_list(coeff(p,x,k-i), i,0,k))$
egf_coeffs([args]) := egf_func('gf_coeffs, args)$


/* ****************************
   * Standard representations *
   ****************************
*/

/* Computing the standard polynomial representation in
   the field f for a given integer polynomial p: */
gf_stand(p) := gf_add(0,p)$
egf_stand(f,p) := egf_func(gf_stand, [f,p])$

/* Deciding whether two integer polynomials p,q represent the
   same element in the field f: */
gf_equalp(p,q) := is(gf_stand(p) = gf_stand(q))$
egf_equalp(f,p,q) := egf_func(gf_equalp, [f,p,q])$


/*!
\endhtmlonly
*/
