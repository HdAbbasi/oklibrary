/* Matthew Gwynne, 21.7.2008 (Swansea) */
/* Copyright 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Graphs/Lisp/Statistics.mac
  \brief Graph Statistics

Use by

oklib_load("Transitional/ComputerAlgebra/Graphs/Lisp/Statistics.mac");

*/

/*!
\htmlonly
*/

oklib_include("Transitional/ComputerAlgebra/DataStructures/Lisp/HashMaps.mac")$
oklib_include("Transitional/ComputerAlgebra/Graphs/Lisp/Basic.mac")$
oklib_include("Transitional/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$


/* ******************
   * Vertex degrees *
   ******************
*/

/* The degree of a vertex: */
degree_g(v,G) := length(neighbours_g(v,G))$
/* For general graphs, counting loops once: */
degree1_gg(v,G) := length(subset(G[2],lambda([e],elementp(v,G[3](e)))))$
/* Counting loops twice: */
degree2_gg(v,G) := gsum_s(
  lambda([el],block([e : G[3](el)], 
   if elementp(v,e) then if length(e) = 1 then 2 else 1
   else 0)),
  G[2])$
/* Remark: If degrees for several vertices are needed, then better the 
   following functions are used.
*/

/* The vertex-degrees of a general graph as hash-map, as a generic function,
   applicable to gg and ogg, counting loops only once: */
vertex_degrees1_genericg(G) := block([h : sm2hm({})],
 for e in G[2] do
   for v in G[3](e) do enter_new_occurrence(h,v),
 for v in G[1] do if ev_hm(h,v) = false then set_hm(h,v,0),
 return(h))$

vertex_degrees_g(G) := vertex_degrees1_genericg(g2gg(G))$
vertex_degrees_og(G) := vertex_degrees1_genericg(og2ogg(G))$
/* For graphs with loops, counting loops once: */
vertex_degrees1_gl(G) := vertex_degrees1_genericg(gl2gg(G))$
vertex_degrees1_ogl(G) := vertex_degrees1_genericg(ogl2ogg(G))$
vertex_degrees1_gg(G) := vertex_degrees1_genericg(G)$
vertex_degrees1_ogg(G) := vertex_degrees1_genericg(G)$

/* The generic version, counting loops twice (again for
   general graphs (ordered or unordered): */
vertex_degrees2_genericg(G) := block([h : sm2hm({})],
 for e in G[2] do
   for v in expand_edge(G[3](e)) do enter_new_occurrence(h,v),
 for v in G[1] do if ev_hm(h,v) = false then set_hm(h,v,0),
 return(h))$

vertex_degrees2_gl(G) := vertex_degrees2_genericg(gl2gg(G))$
vertex_degrees2_ogl(G) := vertex_degrees2_genericg(ogl2ogg(G))$
vertex_degrees2_gg(G) := vertex_degrees2_genericg(G)$
vertex_degrees2_ogg(G) := vertex_degrees2_genericg(G)$


/* Returns the pair [min-degree, vertex_of_minimum_degree], using
   the vertex occurring earliest in the vertex list of the 
   ordered graph. [inf] is returned for the graph
   with empty vertex set. */
min_vertex_degree_v_og(G) := block([vd : vertex_degrees_og(G)],
  first_smallest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$
min_vertex_degree1_v_ogl(G) := block([vd : vertex_degrees1_ogl(G)],
  first_smallest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$
min_vertex_degree2_v_ogl(G) := block([vd : vertex_degrees2_ogl(G)],
  first_smallest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$

max_vertex_degree_v_og(G) := block([vd : vertex_degrees_og(G)],
  first_largest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$
max_vertex_degree1_v_ogl(G) := block([vd : vertex_degrees1_ogl(G)],
  first_largest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$
max_vertex_degree2_v_ogl(G) := block([vd : vertex_degrees2_ogl(G)],
  first_largest_f_l(lambda([v],ev_hm(vd,v)), G[1]))$


/* The hashmap of [vertex degree, frequency] pairs for a graph: */
vertex_degrees_hm_g(G) := block(
  [degrees : hm2sm(vertex_degrees_g(G)), deg_freq : sm2hm({})],
  for vd in degrees do
    enter_new_occurrence(deg_freq, vd[2]),
  return(deg_freq))$


/*!
\endhtmlonly
*/
