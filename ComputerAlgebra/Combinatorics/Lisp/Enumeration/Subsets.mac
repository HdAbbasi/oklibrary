/* Matthew Gwynne, 21.1.2009 (Swansea) */
/* Copyright 2009 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Combinatorics/Lisp/Enumeration/Subsets.mac
  \brief Subset enumeration functions

Use by

oklib_load("OKlib/ComputerAlgebra/Combinatorics/Lisp/Enumeration/Subsets.mac");

*/

/*!
\htmlonly
*/

oklib_include("OKlib/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("OKlib/ComputerAlgebra/DataStructures/Lisp/Lists.mac")$


/* The number of subsets of size k of set M: */
count_ksubsets_s(M,k) := binomial(length(M),k)$
/* The number of k-subsets of {1,...,n}: */
count_ksubsets(n,k) := binomial(n,k)$
/* Prerequisites in both cases: k is an integer. */

/* ***************************
   * Lexicographical ordering *
   ***************************
*/

/* Produces a list of all k-subsets of M in lexicographical order: */
lex_subsets_l(M,k) := if k=0 then [{}] elseif emptyp(M) then [] else
  block([x : first_element(M), R],
    R : disjoin(x,M),
    append(add_element_l(x,lex_subsets_l(R,k-1)), lex_subsets_l(R,k)))$

/* Returns the rank (position) of the subset S of {1,...,n} in the 
   lexicographical enumeration of all |S|-subsets: */
rank_lex_subsets(S,n) := block([L : listify(S), k : length(S)],
  binomial(n,k) - sum_l(map(binomial,n-L,create_list(k-i+1, i,1,k))))$
/*  Note, for lex ordering, this does depend on n (as for instance {2,3,4}
    comes after {1,2,6}).
*/

/* Returns the 'x'th subset in the lexicographical enumeration of all
   k-subsets of {1,...,n}: */ 
unrank_lex_subsets(x,n,k) := block([S : [], L : 1],
  x : binomial(n,k) - x,
  for i : 1 thru k do block([j : k-i+1],
    while binomial(n-L, j) > x do L : L + 1,
    S : endcons(L,S), x : x - binomial(n-L, j)
  ),
  return(setify(S)))$


/* *****************************
   * Colexicographical ordering *
   *****************************
*/

/* Produces a list of all k-subsets of M in colexicographical order: */
colex_subsets_l(M,k) := if k=0 then [{}] elseif emptyp(M) then [] else
  block([x : last_element(M), R],
    R : disjoin(x,M),
    append(colex_subsets_l(R,k), add_element_l(x,colex_subsets_l(R,k-1))))$

/* Returns the rank (position) of the subset S of {1,...,n} in the 
   colexicographical enumeration of all |S|-subsets: */
rank_colex_subsets(S) := block([L : listify(S)],
  sum_l(map(binomial,L-1,create_list(i,i,1,length(L)))) + 1)$
/*  Note, for colex ordering, this does not depend on n. */

/* Returns the 'x'th subset in the colexicographical enumeration of all
   k-subsets of {1,...,n}. */ 
unrank_colex_subsets(x,n,k) := block([S : [], L : n],
  x : x - 1,
  for i : k thru 1 step -1 do (
    while binomial(L-1,i) > x do L : L - 1,
    S : cons(L,S), x : x - binomial(L-1, i)
  ),
  return(setify(S)))$

/*!
\endhtmlonly
*/
