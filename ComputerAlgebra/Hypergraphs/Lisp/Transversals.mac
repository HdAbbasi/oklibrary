/* Oliver Kullmann, 2.2.2008 (Swansea) */
/* Copyright 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Hypergraphs/Lisp/Transversals.mac
  \brief Functions for hypergraph transversals

Use by

oklib_load("Transitional/ComputerAlgebra/Hypergraphs/Lisp/Transversals.mac");

*/

/*!
\htmlonly
*/

oklib_include("Transitional/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$

/* The transversal hypergraph of a hypergraph G */
transversal_hyp(G) := [G[1],transversal_ses(min_elements(G[2]))]$
/* The transversal hypergraph of a set-system S */
transversal_ses(S) := block([M : listify(S),l, h,S1,S2],
 l : length(M),
 if l = 0 then return({{}})
 elseif l = 1 then return(singletons(M[1]))
 elseif l = 2 then return(block([I : intersection(M[1],M[2])],
  union(singletons(I), 
        upairs(setdifference(M[1],I), setdifference(M[2],I))))),
 h : floor(l/2),
 S1 : rest(M,-h),
 S2 : rest(M,l-h),
 return(min_elements(cunion(transversal_ses(S1),transversal_ses(S2)))))$

/* The hypergraph of maximal independent sets of a hypergraph G */
ind_hyp(G) := ecomp_hyp(transversal_hyp(G))$


/*!
\endhtmlonly
*/
