/* Oliver Kullmann, 8.12.2007 (Swansea) */
/* Copyright 2007, 2008 Oliver Kullmann
This file is part of the OKlibrary. OKlibrary is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as published by
the Free Software Foundation and included in this library; either version 3 of the
License, or any later version. */

/*!
  \file ComputerAlgebra/Hypergraphs/Lisp/Basics.mac
  \brief Basic Maxima functions for hypergraphs

Use by

oklib_load("Transitional/ComputerAlgebra/Hypergraphs/Lisp/Basics.mac");

*/

/*!
\htmlonly
*/

oklib_include("Transitional/ComputerAlgebra/Hypergraphs/Lisp/SetSystems.mac")$
oklib_include("Transitional/ComputerAlgebra/CombinatorialMatrices/Lisp/Basics.mac");

/*
 A "hypergraph" is a pair (V,E), where V is a set and E a subset of
 the powerset of V.
*/

/*
 A "general hypergraph" is a triple (V,E,f), where V, E are sets, while
 f is a function such that f(e) for e in E is a subset of V.
*/

/* Equality test for general hypergraphs */
ghypequalp(G1,G2) := is(G1[1] = G2[1]) and is(G1[2] = G2[2]) and
 block([break : false],
  for e in G1[2] unless break do
    if G1[3](e) # G2[3](e) then break : true,
  return(not break))$


/* ************* **
   * Conversions *
   ***************
*/

/* Promotes a set system to a hypergraph: */
setsystem2hg(S) := [set_system_union(S), S]$

/* Converts a list of sets to a hypergraph: */
listsets2hg(L) := setsystem2hg(setify(L))$

/* Converts a list of sets to an ordered general hypergraph: */
listsets2oghg(L) := [listify(list_sets_union(L)), L, identity]$

/* Promotes a hypergraph to a general hypergraph */
promote_general_hypergraph(G) := [G[1], G[2], identity]$

/* The underlying hypergraph of a general hypergraph: */
ghg2hg(G) := [G[1], map(G[3],G[2])]$

/* The combinatorial (hyper)edge-vertex matrix of a hypergraph */
edge_vertex_com_hyp(G) := [G[2], G[1], lambda([H,v], 
  if elementp(v, H) then 1 else 0)]$

/* The (hyper)edge intersection matrix (as a square combinatorial matrix)
   of a hypergraph */
edge_int_com_hyp(G) := block([M : edge_vertex_com_hyp(G)],
 return(com2scom(prod_com(M, trans(M)))))$

/* The vertex intersection matrix (as a square combinatorial matrix)
   of a hypergraph (for each pair of vertices the number of common
   occurrences) */
vertex_int_com_hyp(G) := block([M : edge_vertex_com_hyp(G)],
 return(com2scom(prod_com(trans(M), M))))$



/* *****************
   * Constructions *
   *****************
*/

/* The k-section of a hypergraph */
section_hyp(G,k) := [G[1], family_sets_union(G[2], lambda([H], powerset(H,k)))]$

/* The edge-wise complement of a hypergraph */
ecomp_hyp(G) := [G[1], ecomp(G[2],G[1])]$

/* The closure under subset-formation of a hypergraph */
subset_closure_hyp(G) := [G[1], subset_closure(G[2])]$

/* The dual of a general hypergraph */
dual_general_hypergraph(G) := [G[2], G[1], buildq([V : G[2], f : G[3]],
 lambda([e], subset(V, lambda([v], elementp(e,f(v))))))]$


/* *****************
   * Connectedness *
   *****************
*/

/* Computes the set system of vertex sets of connnected components: */
components_hypergraph(G) := unify_nondisjoint_elements(G[2])$
components_general_hypergraph(G) := components_hypergraph(ghg2hg(G))$

/* whether a hypergraph is connected */
is_connected_hypergraph(G) := is(length(components_hypergraph(G)) <= 1)$


/*!
\endhtmlonly
*/
